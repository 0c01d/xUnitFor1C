Перем КонтекстЯдра;
Перем Ожидаем;

Перем Лог;

Перем RegExp_ПолучитьСписокТестов;
Перем RegExp_КонецФункции_ПолучитьСписокТестов;

//Перем ИмяПараметра_КонтекстЯдра;

//{ Интерфейс конвертера

Функция Инициализация(КонтекстЯдраПараметр) Экспорт
	КонтекстЯдра = КонтекстЯдраПараметр;
	Ожидаем = КонтекстЯдра.Плагин("УтвержденияBDD");
	
	РегулярныеВыражения_Инициализация (RegExp_ПолучитьСписокТестов, "^\s*((?:procedure)|(?:function)|(?:процедура)|(?:функция))\s+(получитьсписоктестов)\s*\(([\wА-яёЁ\d]+)\s*\)\s+экспорт");
	РегулярныеВыражения_Инициализация (RegExp_КонецФункции_ПолучитьСписокТестов, "^\s*конецфункции");
	
	Лог = "";
КонецФункции

Функция ПреобразоватьТекст(Исходный) Экспорт
	
	ОписаниеМетодаПолучитьСписокТестов = ПолучитьОписаниеМетода_ПолучитьСписокТестов(Исходный);
	Если Не ЗначениеЗаполнено(ОписаниеМетодаПолучитьСписокТестов) Тогда
		Возврат "";
	КонецЕсли;
	
	Рез = Новый ТекстовыйДокумент;
	ТекстДоМетодаПолучитьСписокТестов = ЗаменитьГлобальнуюПеременнуюКонтекстЯдра(Лев(Исходный, ОписаниеМетодаПолучитьСписокТестов.Начало-1), ОписаниеМетодаПолучитьСписокТестов.ИмяГлобальнойПеременнойКонтекстаЯдра);
	ДобавитьЛог("текст до ПолучитьСписокТестов"+Символы.ПС+ТекстДоМетодаПолучитьСписокТестов );
	Рез.ДобавитьСтроку(ТекстДоМетодаПолучитьСписокТестов);
	
	Рез.ДобавитьСтроку(ДобавитьКомментарии("Перем " + ОписаниеМетодаПолучитьСписокТестов.ИмяГлобальнойПеременнойКонтекстаЯдра + ";"));
	
	//ИсходныйТекстВКомментарии = "// Перем " + ОписаниеМетодаПолучитьСписокТестов.ИмяГлобальнойПеременнойКонтекстаЯдра + ";"+Символы.ПС;
	ИсходныйТекстВКомментарии = ДобавитьКомментарии(Сред(Исходный, ОписаниеМетодаПолучитьСписокТестов.Начало, ОписаниеМетодаПолучитьСписокТестов.Конец-ОписаниеМетодаПолучитьСписокТестов.Начало));
	
	Промежуточный = ЗаменитьГлобальнуюПеременнуюКонтекстЯдра(Сред(Исходный, ОписаниеМетодаПолучитьСписокТестов.Конец), ОписаниеМетодаПолучитьСписокТестов.ИмяГлобальнойПеременнойКонтекстаЯдра);
	Рез.ДобавитьСтроку(ИсходныйТекстВКомментарии);
	
	ДобавитьЛог("Промежуточный (после замены переменной контекст ядра) "+Символы.ПС+Промежуточный );
	Рез.ДобавитьСтроку(Промежуточный);
	
	Макет = ПолучитьМакет("ШаблонТеста");
	Макет.Вывести(Рез);
	
	Рез.Вывести();
	Возврат Макет.ПолучитьТекст();
КонецФункции

Функция ПолучитьЛог() Экспорт
	Возврат Лог;
КонецФункции

//}

//{ приватные методы

Функция ПолучитьОписаниеМетода_ПолучитьСписокТестов(ИсходныйТекст) Экспорт
	ОписаниеМетода = Новый Структура("Начало, Конец, ИмяПараметра_КонтекстЯдра, ТелоМетода, КоллекцияТестов");
	
	Группировки = РегулярныеВыражения_Выполнить(RegExp_ПолучитьСписокТестов, ИсходныйТекст);
	Если Не ЗначениеЗаполнено(Группировки) Тогда
		ДобавитьЛог("Не удалось найти экспортную процедуру ПолучитьСписокТестов с одним параметром.");
		Возврат Неопределено;
	КонецЕсли;
	
	Группировка_Процедура = Группировки[0];
	Ожидаем.Что(Группировка_Процедура.ПодВыражения.Количество(), "Ожидаем, что количество найденных элементов при поиске ПолучитьСписокТестов равно образцу, а это не так").Равно(3);
	ОписаниеМетода.Вставить("Начало", Группировка_Процедура.Начало + 2);
	ОписаниеМетода.Вставить("ИмяПараметра_КонтекстЯдра", Группировка_Процедура.ПодВыражения[2]);
	ДобавитьЛог("ОписаниеМетода.ИмяПараметра_КонтекстЯдра "+ОписаниеМетода.ИмяПараметра_КонтекстЯдра);
	
	НачалоКодаПроцедуры = Группировка_Процедура.Начало + Группировка_Процедура.Длина + 2;
	
	Группировки_КонецПроцедуры = РегулярныеВыражения_Выполнить(RegExp_КонецФункции_ПолучитьСписокТестов, Сред(ИсходныйТекст, НачалоКодаПроцедуры));
	Если Не ЗначениеЗаполнено(Группировки_КонецПроцедуры) Тогда
		ДобавитьЛог("Не удалось найти конец процедуры для процедуры ПолучитьСписокТестов с одним параметром.");
		Возврат Неопределено;
	КонецЕсли;
	
	ГруппировкаДляКонецПроцедуры = Группировки_КонецПроцедуры[0];
	ТекстМетодаПолучитьСписокТестов = Сред(ИсходныйТекст, НачалоКодаПроцедуры, ГруппировкаДляКонецПроцедуры.Начало - 1); 
	
	ОписаниеМетода.Вставить("Конец", НачалоКодаПроцедуры + ГруппировкаДляКонецПроцедуры.Начало + ГруппировкаДляКонецПроцедуры.Длина + 2);
	ОписаниеМетода.Вставить("ТелоМетода", ТекстМетодаПолучитьСписокТестов);
	
	ДобавитьЛог("ТелоМетода ПолучитьСписокТестов <"+ОписаниеМетода.ТелоМетода+">");
	
	ИмяГлобальнойПеременнойКонтекстаЯдра = ПолучитьИмяГлобальнойПеременнойКонтекстаЯдра(ИсходныйТекст, ОписаниеМетода);
	ИмяГлобальнойПеременнойКонтекстаЯдра = ПодтвердитьИмяГлобальнойПеременнойКонтекстаЯдра(ИсходныйТекст, ОписаниеМетода, ИмяГлобальнойПеременнойКонтекстаЯдра);
	ОписаниеМетода.Вставить("ИмяГлобальнойПеременнойКонтекстаЯдра", ИмяГлобальнойПеременнойКонтекстаЯдра);
	
	КоллекцияТестов = ПолучитьКоллекциюТестов(ИсходныйТекст, ОписаниеМетода);
	ОписаниеМетода.Вставить("КоллекцияТестов", КоллекцияТестов);
	
	Возврат ОписаниеМетода;
КонецФункции

Функция ПолучитьИмяГлобальнойПеременнойКонтекстаЯдра(ИсходныйТекст, ОписаниеМетода)
	RegExp_ПрисваиваниеКонтекстаЯдра = Неопределено;
	РегулярныеВыражения_Инициализация (RegExp_ПрисваиваниеКонтекстаЯдра, "^\s*([\wА-яёЁ\d]+)\s*=\s*" + ОписаниеМетода.ИмяПараметра_КонтекстЯдра + "\s*;");
	Группировки = РегулярныеВыражения_Выполнить(RegExp_ПрисваиваниеКонтекстаЯдра, ОписаниеМетода.ТелоМетода);
	Если Не ЗначениеЗаполнено(Группировки) Тогда
		ДобавитьЛог("Не удалось найти присваивание параметра контекста ядра.");
		Возврат Неопределено;
	КонецЕсли;
	Ожидаем.Что(Группировки[0].ПодВыражения.Количество(), "Ожидаем, что количество найденных элементов при поиске ИмяГлобальнойПеременнойКонтекстаЯдра равно образцу, а это не так").Равно(1);
	ИмяГлобальнойПеременнойКонтекстаЯдра = Группировки[0].ПодВыражения[0];
	ДобавитьЛог("предварительно ИмяГлобальнойПеременнойКонтекстаЯдра = "+ИмяГлобальнойПеременнойКонтекстаЯдра);
	Возврат ИмяГлобальнойПеременнойКонтекстаЯдра;
КонецФункции

Функция ПодтвердитьИмяГлобальнойПеременнойКонтекстаЯдра(ИсходныйТекст, ОписаниеМетода, ИмяГлобальнойПеременнойКонтекстаЯдра)
	RegExp_ОбъявлениеГлобальнойПеременнойКонтекстаЯдра = Неопределено;
	РегулярныеВыражения_Инициализация (RegExp_ОбъявлениеГлобальнойПеременнойКонтекстаЯдра, "^\s*Перем\s+(" + ИмяГлобальнойПеременнойКонтекстаЯдра + ")\s*[;,]");
	Группировки = РегулярныеВыражения_Выполнить(RegExp_ОбъявлениеГлобальнойПеременнойКонтекстаЯдра, ИсходныйТекст);
	Если Не ЗначениеЗаполнено(Группировки) Тогда
		ДобавитьЛог("Не удалось найти глобальную переменную контекста ядра.");
		Возврат Неопределено;
	КонецЕсли;
	Ожидаем.Что(Группировки[0].ПодВыражения.Количество(), "Ожидаем, что количество найденных элементов при проверке ИмяГлобальнойПеременнойКонтекстаЯдра равно образцу, а это не так").Равно(1);
	ИмяГлобальнойПеременнойКонтекстаЯдра = Группировки[0].ПодВыражения[0];
	ДобавитьЛог("подтверждено ИмяГлобальнойПеременнойКонтекстаЯдра = "+ИмяГлобальнойПеременнойКонтекстаЯдра);
	Возврат ИмяГлобальнойПеременнойКонтекстаЯдра;
КонецФункции

Функция ПолучитьКоллекциюТестов(ИсходныйТекст, ОписаниеМетода)
	RegExp_ИмяКоллекцииТестов = Неопределено;
	//РегулярныеВыражения_Инициализация (RegExp_ИмяКоллекцииТестов, "\s*([\wА-яёЁ\d]+)\s*=\s*Новый\s+Массив\s*[;\(]([.\s].+)+Возврат\s+([\wА-яёЁ\d]+)");
	РегулярныеВыражения_Инициализация (RegExp_ИмяКоллекцииТестов, "^\s*Возврат\s+([\wА-яёЁ\d]+)");
	Группировки = РегулярныеВыражения_Выполнить(RegExp_ИмяКоллекцииТестов, ОписаниеМетода.ТелоМетода);
	Если Не ЗначениеЗаполнено(Группировки) Тогда
		ДобавитьЛог("Не удалось найти создание коллекции тестов.");
		Возврат Неопределено;
	КонецЕсли;
	Ожидаем.Что(Группировки[0].ПодВыражения.Количество(), "Ожидаем, что количество найденных элементов при поиске ИмяГлобальнойПеременнойКонтекстаЯдра равно образцу, а это не так").Равно(1);
	ИмяПеременнойКоллекцииСпискаТестов = Группировки[0].ПодВыражения[0];
	ДобавитьЛог("ИмяПеременнойКоллекцииСпискаТестов = "+ИмяПеременнойКоллекцииСпискаТестов);
	
	RegExp_ДобавлениеТеста = Неопределено;
	РегулярныеВыражения_Инициализация (RegExp_ДобавлениеТеста, "^\s*" + ИмяПеременнойКоллекцииСпискаТестов + "\s*\.\s*Добавить\(\s*""([\wА-яёЁ\d]+)""\s*\)\s*;");
	Группировки = РегулярныеВыражения_Выполнить(RegExp_ДобавлениеТеста, ОписаниеМетода.ТелоМетода);
	Если Не ЗначениеЗаполнено(Группировки) Тогда
		ДобавитьЛог("Не удалось найти добавление тестов.");
		Возврат Неопределено;
	КонецЕсли;
	Ожидаем.Что(Группировки[0].ПодВыражения.Количество(), "Ожидаем, что количество найденных элементов при поиске ИмяГлобальнойПеременнойКонтекстаЯдра равно образцу, а это не так").Равно(1);
	
	КоллекцияТестов = Новый  Массив;
	Для Каждого Подвыражение Из Группировки[0].ПодВыражения Цикл
		ДобавитьЛог("добавляем имя теста = "+Подвыражение);
		КоллекцияТестов.Добавить(Подвыражение);
	КонецЦикла;
	
	Возврат КоллекцияТестов;
КонецФункции

Функция ЗаменитьГлобальнуюПеременнуюКонтекстЯдра(Исходный, ИмяГлобальнойПеременнойКонтекстаЯдра)
	RegExp_ИмяГлобальнойПеременнойКонтекстаЯдра = Неопределено;
	//РегулярныеВыражения_Инициализация (RegExp_ИмяГлобальнойПеременнойКонтекстаЯдра, "\s+(" + ИмяГлобальнойПеременнойКонтекстаЯдра + ")[\.\s;]+");
	РегулярныеВыражения_Инициализация (RegExp_ИмяГлобальнойПеременнойКонтекстаЯдра, "(\s+)(" + ИмяГлобальнойПеременнойКонтекстаЯдра + ")([\.\s;]+)+");
	Рез = РегулярныеВыражения_Заменить(RegExp_ИмяГлобальнойПеременнойКонтекстаЯдра, Исходный, "$1КонтекстЯдра$3");
	//ДобавитьЛог("Рез = "+Символы.ПС+ Рез);
	Возврат Рез;
КонецФункции

Функция ДобавитьКомментарии(Строка)
	Рез = "";
	Для к=1 По СтрЧислоСтрок(Строка) Цикл
		Рез = Рез + "// "+СтрПолучитьСтроку(Строка, к) + Символы.ПС;
	КонецЦикла;
	Возврат Рез; //Лев(Рез, СтрДлина(Рез) - 2);
КонецФункции

Процедура ДобавитьЛог(Сообщение)
	Лог = Лог + Сообщение + Символы.ПС;
КонецПроцедуры

//}

//{ RegExp

Процедура РегулярныеВыражения_Инициализация (RegExp, Шаблон, ИскатьДоПервогоСовпадения = Ложь, МногоСтрок = Истина, ИгнорироватьРегистр = Истина) Экспорт

    Если RegExp = Неопределено Тогда //Нужна инициализация
        RegExp = Новый COMОбъект("VBScript.RegExp");    // создаем объект для работы с регулярными выражениями
    КонецЕсли;

    //Заполняем данные
    RegExp.MultiLine = МногоСтрок;                  // истина — текст многострочный, ложь — одна строка
    RegExp.Global = Не ИскатьДоПервогоСовпадения;   // истина — поиск по всей строке, ложь — до первого совпадения
    RegExp.IgnoreCase = ИгнорироватьРегистр;        // истина — игнорировать регистр строки при поиске
    RegExp.Pattern = Шаблон;                        // шаблон (регулярное выражение)

КонецПроцедуры

Функция РегулярныеВыражения_Проверка(RegExp, ПроверяемыйТекст)

    Возврат RegExp.Test(ПроверяемыйТекст);

КонецФункции

Функция РегулярныеВыражения_Выполнить(RegExp, АнализируемыйТекст) Экспорт

    РезультатАнализаСтроки = RegExp.Execute(АнализируемыйТекст);

    Группировки = Новый Массив;

    Для Каждого Выражение Из РезультатАнализаСтроки Цикл
        СтруктураВыражение = Новый Структура ("Начало, Длина, Значение, ПодВыражения", Выражение.FirstIndex, Выражение.Length,Выражение.Value);

        //Обработка подвыражений
        МассивПодВыражений = Новый Массив;
        Для Каждого ПодВыражение Из Выражение.SubMatches Цикл
            МассивПодВыражений.Добавить(ПодВыражение);
        КонецЦикла;
        СтруктураВыражение.ПодВыражения = МассивПодВыражений;

        Группировки.Добавить (СтруктураВыражение);

    КонецЦикла;

    Возврат Группировки;

КонецФункции

Функция РегулярныеВыражения_Заменить(RegExp, АнализируемыйТекст, ЗаменяемыйТекст) Экспорт

    Рез = RegExp.Replace(АнализируемыйТекст, ЗаменяемыйТекст);
    Возврат Рез;

КонецФункции

//}