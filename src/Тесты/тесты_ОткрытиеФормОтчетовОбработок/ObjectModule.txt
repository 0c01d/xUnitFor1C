Перем _Форма;

Перем	юТест;

// основная процедура для юнит-тестирования SnowTest
Функция ПолучитьСписокТестов(ЮнитТестирование) Экспорт
	юТест = ЮнитТестирование;
	
	ВсеТесты = Новый Массив;

	ДобавитьТестДляМетаданного(ВсеТесты, Метаданные.Отчеты, "Тест_ОткрытьФормуОтчета", ПолучитьСписокИсключений_Отчеты());
	
	ДобавитьТестДляМетаданного(ВсеТесты, Метаданные.Обработки, "Тест_ОткрытьФормуОбработки", ПолучитьСписокИсключений_Обработки());
	
	Возврат ВсеТесты;
КонецФункции

Процедура ПослеЗапускаТеста() Экспорт
	Если ТипЗнч(_Форма) <> Тип("Форма") Тогда
		Возврат;
	КонецЕсли; 
	_Форма.Модифицированность = Ложь;
	Если _Форма.Открыта() Тогда
		_Форма.Закрыть();
	Иначе
		Попытка
			_Форма.Закрыть();
		Исключение
		КонецПопытки;
	КонецЕсли;
	_Форма = "";
КонецПроцедуры

Процедура ДобавитьТестДляМетаданного(ВсеТесты, менеджерМетаданного, имяТеста, списокИсключений)
	Для Каждого мДокумент Из менеджерМетаданного Цикл
		Если списокИсключений.НайтиПоЗначению(мДокумент.Имя) <> Неопределено Тогда
			Продолжить;
		КонецЕсли;
		Если ПравоДоступа("Просмотр", мДокумент) Тогда
			лПараметры = Новый Структура("ИмяТеста,Транзакция,Параметр", имяТеста, Истина, мДокумент.Имя);
			ВсеТесты.Добавить(лПараметры);
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

Процедура Тест_ОткрытьФормуОтчета(_ИД) Экспорт
	объект = Отчеты[_ИД].Создать();
	_Форма = объект.ПолучитьФорму();
	
	_Форма.Открыть();
		юТест.Проверить(_Форма.Открыта(), "_Форма отчета """+_ИД+""" не открылась, а должна была открыться");
	_Форма.Обновить();
КонецПроцедуры

Процедура Тест_ОткрытьФормуОбработки(_ИД) Экспорт
	объект = Обработки[_ИД].Создать();
	_Форма = объект.ПолучитьФорму();
	
	_Форма.Открыть();
		юТест.Проверить(_Форма.Открыта(), "_Форма обработки """+_ИД+""" не открылась, а должна была открыться");
	_Форма.Обновить();
КонецПроцедуры

Функция ПолучитьСписокИсключений_Отчеты()
	Результат = Новый СписокЗначений;
	
	//Результат.Добавить("Цены");
	
	Возврат Результат;
КонецФункции

Функция ПолучитьСписокИсключений_Обработки()
	Результат = Новый СписокЗначений;
	
	//Результат.Добавить("Цены");
	
	Возврат Результат;
КонецФункции
