
//{ основная процедура для юнит-тестирования xUnitFor1C
Перем ЮТест;

Функция ПолучитьСписокТестов(ЮнитТестирование) Экспорт
	
	ЮТест = ЮнитТестирование;
	
	ВсеТесты = Новый Массив;

	ПрефиксПредставления = "";
	ДополнитьСписокТестов(ВсеТесты, ПутьЗапускателяТестов(), ПрефиксПредставления);

	ВсеТесты.Добавить("ТестДолжен_ОбработатьПустойПараметрЗапуска");
	ВсеТесты.Добавить("ТестДолжен_ОбработатьТиповыеПараметрыЗапуска");
	ВсеТесты.Добавить("ТестДолжен_ОбработатьТиповыеПараметрыЗапуска_АнглийскиеКоманды");
	ВсеТесты.Добавить("ТестДолжен_ОбработатьПараметрыЗапускаБезКомандВыполнитьИлиЗагрузить");
	ВсеТесты.Добавить("ТестДолжен_ОбработатьПараметрыЗапускаБезКомандВыполнитьИлиЗагрузить_АнглийскиеКоманды");
	ВсеТесты.Добавить("ТестДолжен_ОбработатьВсеТиповыеПараметрыЗапуска");
	ВсеТесты.Добавить("ТестДолжен_ОбработатьВсеТиповыеПараметрыЗапуска_АнглийскиеКоманды");
	ВсеТесты.Добавить("ТестДолжен_ОбработатьВсеТиповыеПараметрыЗапуска_и_Запись_в_ЖР");
	ВсеТесты.Добавить("ТестДолжен_ОбработатьВсеТиповыеПараметрыЗапуска_и_Запись_в_ЖР_АнглийскиеКоманды");
	ВсеТесты.Добавить("ТестДолжен_ОбработатьВсеТиповыеПараметрыЗапуска_ЕстьПробелыСпередиИСзадиКоманды");
	ВсеТесты.Добавить("ТестДолжен_ОбработатьВсеТиповыеПараметрыЗапускаДляВстроенныхТестов");
	ВсеТесты.Добавить("ТестДолжен_ОбработатьВсеТиповыеПараметрыЗапускаДляВстроенныхТестов_АнглийскиеКоманды");

	Возврат ВсеТесты;
	
КонецФункции

Функция ДополнитьСписокТестов(ВсеТесты, ПутьЗапускателяТестов, ПрефиксПредставления = "") Экспорт
	ТекущийПользователь = ПользователиИнформационнойБазы.ТекущийПользователь();
	
	ПутьТестов = ПутьЗапускателяТестов+"TestsShouldFail\ТестПримерСломаногоТеста.epf";
	КоличествоУпавшихТестов = 1;
	
	ИмяПользователя = ТекущийПользователь.Имя;
	РежимЗапуска = РежимЗапускаКлиентскогоПриложения.ОбычноеПриложение;
	Настройка = Новый Структура("ИмяПользователя,ИмяМакета,ЗапускатьТолстыйКлиент,ПутьТестов,КоличествоУпавшихТестов,РежимЗапуска", 
		ИмяПользователя, "", Истина, ПутьТестов, КоличествоУпавшихТестов, РежимЗапуска);
	ПараметрыТеста = Новый Структура("ИмяТеста,ПредставлениеТеста,Параметр", "ВыполнитьОдинТестЧерезЗапуск1СПредприятия", 
		ПрефиксПредставления + "Протестить падающий тест в отдельном сеансе пользователя <"+ИмяПользователя+"> (Обычное приложение)", Настройка);
	ВсеТесты.Добавить(ПараметрыТеста);
	
	РежимЗапуска = РежимЗапускаКлиентскогоПриложения.УправляемоеПриложение;
	Настройка = Новый Структура("ИмяПользователя,ИмяМакета,ЗапускатьТолстыйКлиент,ПутьТестов,КоличествоУпавшихТестов,РежимЗапуска", 
		ИмяПользователя, "", Ложь, ПутьТестов, КоличествоУпавшихТестов, РежимЗапуска);
	ПараметрыТеста = Новый Структура("ИмяТеста,ПредставлениеТеста,Параметр", "ВыполнитьОдинТестЧерезЗапуск1СПредприятия", 
		ПрефиксПредставления + "Протестить падающий тест в отдельном сеансе пользователя <"+ИмяПользователя+"> (Тонкий клиент)", Настройка);
	ВсеТесты.Добавить(ПараметрыТеста);
	
	Настройка = Новый Структура("ИмяПользователя,ИмяМакета,ЗапускатьТолстыйКлиент,ПутьТестов,КоличествоУпавшихТестов,РежимЗапуска", 
		ИмяПользователя, "", Истина, ПутьТестов, КоличествоУпавшихТестов, РежимЗапуска);
	ПараметрыТеста = Новый Структура("ИмяТеста,ПредставлениеТеста,Параметр", "ВыполнитьОдинТестЧерезЗапуск1СПредприятия", 
		ПрефиксПредставления + "Протестить падающий тест в отдельном сеансе пользователя <"+ИмяПользователя+"> (Толстый клиент управляемое приложение)", Настройка);
	ВсеТесты.Добавить(ПараметрыТеста);
	
	ПутьТестов = ПутьЗапускателяТестов+"Tests\selftests\ТестыПроверитьРавенство.epf";
	КоличествоУпавшихТестов = 0;
	
	РежимЗапуска = РежимЗапускаКлиентскогоПриложения.ОбычноеПриложение;
	Настройка = Новый Структура("ИмяПользователя,ИмяМакета,ЗапускатьТолстыйКлиент,ПутьТестов,КоличествоУпавшихТестов,РежимЗапуска", 
		ИмяПользователя, "", Истина, ПутьТестов, КоличествоУпавшихТестов, РежимЗапуска);
	ПараметрыТеста = Новый Структура("ИмяТеста,ПредставлениеТеста,Параметр", "ВыполнитьОдинТестЧерезЗапуск1СПредприятия", 
		ПрефиксПредставления + "Протестить успешный тест в отдельном сеансе пользователя <"+ИмяПользователя+"> (Обычное приложение)", Настройка);
	ВсеТесты.Добавить(ПараметрыТеста);
	
	РежимЗапуска = РежимЗапускаКлиентскогоПриложения.УправляемоеПриложение;
	Настройка = Новый Структура("ИмяПользователя,ИмяМакета,ЗапускатьТолстыйКлиент,ПутьТестов,КоличествоУпавшихТестов,РежимЗапуска",  
		ИмяПользователя, "", Ложь, ПутьТестов, КоличествоУпавшихТестов, РежимЗапуска);
	ПараметрыТеста = Новый Структура("ИмяТеста,ПредставлениеТеста,Параметр", "ВыполнитьОдинТестЧерезЗапуск1СПредприятия", 
		ПрефиксПредставления + "Протестить успешный тест в отдельном сеансе пользователя <"+ИмяПользователя+"> (Тонкий клиент)", Настройка);
	ВсеТесты.Добавить(ПараметрыТеста);
	
	Настройка = Новый Структура("ИмяПользователя,ИмяМакета,ЗапускатьТолстыйКлиент,ПутьТестов,КоличествоУпавшихТестов,РежимЗапуска", 
		ИмяПользователя, "", Истина, ПутьТестов, КоличествоУпавшихТестов, РежимЗапуска);
	ПараметрыТеста = Новый Структура("ИмяТеста,ПредставлениеТеста,Параметр", "ВыполнитьОдинТестЧерезЗапуск1СПредприятия", 
		ПрефиксПредставления + "Протестить успешный тест в отдельном сеансе пользователя <"+ИмяПользователя+"> (Толстый клиент управляемое приложение)", Настройка);
	ВсеТесты.Добавить(ПараметрыТеста);
	
	ПутьТестов = "Метаданные.Обработки.Тест_Обработка";
	//ПутьТестов = "Метаданные.Подсистемы.Тестовая"; //"Метаданные.Обработки.Тест_Обработка"
	КоличествоУпавшихТестов = 0;
	
	РежимЗапуска = РежимЗапускаКлиентскогоПриложения.ОбычноеПриложение;
	Настройка = Новый Структура("ИмяПользователя,ИмяМакета,ЗапускатьТолстыйКлиент,ПутьТестов,КоличествоУпавшихТестов,РежимЗапуска", 
		ИмяПользователя, "", Истина, ПутьТестов, КоличествоУпавшихТестов, РежимЗапуска);
	ПараметрыТеста = Новый Структура("ИмяТеста,ПредставлениеТеста,Параметр", "ВыполнитьОдинТестЧерезЗапуск1СПредприятия", 
		ПрефиксПредставления + "Протестить успешный встроенный тест в отдельном сеансе пользователя <"+ИмяПользователя+"> (Обычное приложение)", Настройка);
	ВсеТесты.Добавить(ПараметрыТеста);
	
	ПутьТестов = "Метаданные.Подсистемы.Тестовая"; //"Метаданные.Обработки.Тест_Обработка"
	КоличествоУпавшихТестов = 0;
	
	РежимЗапуска = РежимЗапускаКлиентскогоПриложения.ОбычноеПриложение;
	Настройка = Новый Структура("ИмяПользователя,ИмяМакета,ЗапускатьТолстыйКлиент,ПутьТестов,КоличествоУпавшихТестов,РежимЗапуска", 
		ИмяПользователя, "", Истина, ПутьТестов, КоличествоУпавшихТестов, РежимЗапуска);
	ПараметрыТеста = Новый Структура("ИмяТеста,ПредставлениеТеста,Параметр", "ВыполнитьОдинТестЧерезЗапуск1СПредприятия", 
		ПрефиксПредставления + "Протестить несколько успешных встроенных тестов в отдельном сеансе пользователя <"+ИмяПользователя+"> (Обычное приложение)", Настройка);
	ВсеТесты.Добавить(ПараметрыТеста);
КонецФункции

//}

//{ блок юнит-тестов - сами тесты

Процедура ПередЗапускомТеста() Экспорт
	
КонецПроцедуры

Процедура ПослеЗапускаТеста() Экспорт
КонецПроцедуры

Процедура ТестДолжен_ОбработатьПустойПараметрЗапуска() Экспорт
	СтрокаПараметров = "";
	
	ПараметрыЗапуска = РазобратьПараметрыЗапуска(СтрокаПараметров);
	
	ЮТест.ПроверитьРавенство(ПараметрыЗапуска, Неопределено, "ПараметрыЗапуска");
КонецПроцедуры

Процедура ТестДолжен_ОбработатьТиповыеПараметрыЗапуска() Экспорт
	СтрокаПараметров = "Тесты_Команда_Тестировать;E:\ТестыИМ\Тесты;ЗавершитьРаботуПослеТестирования";
	
	ПараметрыЗапуска = РазобратьПараметрыЗапуска(СтрокаПараметров);
	
	ЮТест.ПроверитьРавенство(ПараметрыЗапуска.Выполнить, Истина, "ПараметрыЗапуска.Выполнить");
	ЮТест.ПроверитьРавенство(ПараметрыЗапуска.ПутьТестов, "E:\ТестыИМ\Тесты", "ПараметрыЗапуска.ПутьТестов");
	ЮТест.ПроверитьРавенство(ПараметрыЗапуска.ОстановитьСистему, Истина, "ПараметрыЗапуска.ОстановитьСистему");
	
	ЮТест.ПроверитьРавенство(ПараметрыЗапуска.ФорматОтчета, "", "ПараметрыЗапуска.ФорматОтчета");
	ЮТест.ПроверитьРавенство(ПараметрыЗапуска.ПутьОтчета, "", "ПараметрыЗапуска.ПутьОтчета");
	ЮТест.ПроверитьРавенство(ПараметрыЗапуска.Загрузить, Истина, "ПараметрыЗапуска.Загрузить");
	ЮТест.ПроверитьРавенство(ПараметрыЗапуска.ВстроенныеТесты, Ложь, "ПараметрыЗапуска.ВстроенныеТесты");
	ЮТест.ПроверитьРавенство(ПараметрыЗапуска.ПутьКодаВозврата, "", "ПараметрыЗапуска.ПутьКодаВозврата");
	ЮТест.ПроверитьРавенство(ПараметрыЗапуска.РегистрацияПрохожденияТестовВЖР, Ложь, "ПараметрыЗапуска.xddWriteLogEvent");
КонецПроцедуры

Процедура ТестДолжен_ОбработатьТиповыеПараметрыЗапуска_АнглийскиеКоманды() Экспорт
	СтрокаПараметров = "xddRun;E:\ТестыИМ\Тесты;xddShutdown";
	
	ПараметрыЗапуска = РазобратьПараметрыЗапуска(СтрокаПараметров);
	
	ЮТест.ПроверитьРавенство(ПараметрыЗапуска.Выполнить, Истина, "ПараметрыЗапуска.Выполнить");
	ЮТест.ПроверитьРавенство(ПараметрыЗапуска.ПутьТестов, "E:\ТестыИМ\Тесты", "ПараметрыЗапуска.ПутьТестов");
	ЮТест.ПроверитьРавенство(ПараметрыЗапуска.ОстановитьСистему, Истина, "ПараметрыЗапуска.ОстановитьСистему");
	
	ЮТест.ПроверитьРавенство(ПараметрыЗапуска.ФорматОтчета, "", "ПараметрыЗапуска.ФорматОтчета");
	ЮТест.ПроверитьРавенство(ПараметрыЗапуска.ПутьОтчета, "", "ПараметрыЗапуска.ПутьОтчета");
	ЮТест.ПроверитьРавенство(ПараметрыЗапуска.Загрузить, Истина, "ПараметрыЗапуска.Загрузить");
	ЮТест.ПроверитьРавенство(ПараметрыЗапуска.ВстроенныеТесты, Ложь, "ПараметрыЗапуска.ВстроенныеТесты");
	ЮТест.ПроверитьРавенство(ПараметрыЗапуска.ПутьКодаВозврата, "", "ПараметрыЗапуска.ПутьКодаВозврата");
	ЮТест.ПроверитьРавенство(ПараметрыЗапуска.РегистрацияПрохожденияТестовВЖР, Ложь, "ПараметрыЗапуска.xddWriteLogEvent");
КонецПроцедуры

Процедура ТестДолжен_ОбработатьПараметрыЗапускаБезКомандВыполнитьИлиЗагрузить() Экспорт
	СтрокаПараметров = "E:\ТестыИМ\Тесты;ЗавершитьРаботуПослеТестирования";
	
	ПараметрыЗапуска = РазобратьПараметрыЗапуска(СтрокаПараметров);
	
	ЮТест.ПроверитьРавенство(ПараметрыЗапуска, Неопределено, "ПараметрыЗапуска");
КонецПроцедуры

Процедура ТестДолжен_ОбработатьПараметрыЗапускаБезКомандВыполнитьИлиЗагрузить_АнглийскиеКоманды() Экспорт
	СтрокаПараметров = "E:\ТестыИМ\Тесты;xddShutdown";
	
	ПараметрыЗапуска = РазобратьПараметрыЗапуска(СтрокаПараметров);
	
	ЮТест.ПроверитьРавенство(ПараметрыЗапуска, Неопределено, "ПараметрыЗапуска");
КонецПроцедуры

Процедура ТестДолжен_ОбработатьВсеТиповыеПараметрыЗапуска() Экспорт
	СтрокаПараметров = "Тесты_Команда_Тестировать;E:\Путь с пробелом\Tests;ЗавершитьРаботуПослеТестирования;Тесты_Команда_ФорматОтчета;xml;Тесты_Команда_КаталогОтчетов;E:\Путь с пробелом\testReport;Тесты_Путь_КЛогам;E:\f1.log";
	
	ПараметрыЗапуска = РазобратьПараметрыЗапуска(СтрокаПараметров);
	
	ЮТест.ПроверитьРавенство(ПараметрыЗапуска.Выполнить, Истина, "ПараметрыЗапуска.Выполнить");
	ЮТест.ПроверитьРавенство(ПараметрыЗапуска.ПутьТестов, "E:\Путь с пробелом\Tests", "ПараметрыЗапуска.ПутьТестов");
	ЮТест.ПроверитьРавенство(ПараметрыЗапуска.ОстановитьСистему, Истина, "ПараметрыЗапуска.ОстановитьСистему");
	ЮТест.ПроверитьРавенство(ПараметрыЗапуска.ФорматОтчета, "xml", "ПараметрыЗапуска.ФорматОтчета");
	ЮТест.ПроверитьРавенство(ПараметрыЗапуска.ПутьОтчета, "E:\Путь с пробелом\testReport\", "ПараметрыЗапуска.ПутьОтчета");
	
	ЮТест.ПроверитьРавенство(ПараметрыЗапуска.Загрузить, Истина, "ПараметрыЗапуска.Загрузить");
	ЮТест.ПроверитьРавенство(ПараметрыЗапуска.ВстроенныеТесты, Ложь, "ПараметрыЗапуска.ВстроенныеТесты");
	ЮТест.ПроверитьРавенство(ПараметрыЗапуска.ПутьКодаВозврата, "E:\f1.log", "ПараметрыЗапуска.ПутьКодаВозврата");
КонецПроцедуры

Процедура ТестДолжен_ОбработатьВсеТиповыеПараметрыЗапуска_АнглийскиеКоманды() Экспорт
	СтрокаПараметров = "xddRun;E:\Путь с пробелом\Tests;xddShutdown;xddReportFormat;xml;xddReportPath;E:\Путь с пробелом\testReport;xddExitCodePath;E:\f1.log";
	
	ПараметрыЗапуска = РазобратьПараметрыЗапуска(СтрокаПараметров);
	
	ЮТест.ПроверитьРавенство(ПараметрыЗапуска.Выполнить, Истина, "ПараметрыЗапуска.Выполнить");
	ЮТест.ПроверитьРавенство(ПараметрыЗапуска.ПутьТестов, "E:\Путь с пробелом\Tests", "ПараметрыЗапуска.ПутьТестов");
	ЮТест.ПроверитьРавенство(ПараметрыЗапуска.ОстановитьСистему, Истина, "ПараметрыЗапуска.ОстановитьСистему");
	ЮТест.ПроверитьРавенство(ПараметрыЗапуска.ФорматОтчета, "xml", "ПараметрыЗапуска.ФорматОтчета");
	ЮТест.ПроверитьРавенство(ПараметрыЗапуска.ПутьОтчета, "E:\Путь с пробелом\testReport\", "ПараметрыЗапуска.ПутьОтчета");
	
	ЮТест.ПроверитьРавенство(ПараметрыЗапуска.Загрузить, Истина, "ПараметрыЗапуска.Загрузить");
	ЮТест.ПроверитьРавенство(ПараметрыЗапуска.ВстроенныеТесты, Ложь, "ПараметрыЗапуска.ВстроенныеТесты");
	ЮТест.ПроверитьРавенство(ПараметрыЗапуска.ПутьКодаВозврата, "E:\f1.log", "ПараметрыЗапуска.ПутьКодаВозврата");
КонецПроцедуры

Процедура ТестДолжен_ОбработатьВсеТиповыеПараметрыЗапуска_и_Запись_в_ЖР() Экспорт
	СтрокаПараметров = "Тесты_Команда_Тестировать;E:\Путь с пробелом\Tests;ЗавершитьРаботуПослеТестирования;Тесты_Команда_ФорматОтчета;xml;Тесты_Команда_КаталогОтчетов;E:\Путь с пробелом\testReport;Тесты_Путь_КЛогам;E:\f1.log;Тесты_ЗаписьВЖР";
	
	ПараметрыЗапуска = РазобратьПараметрыЗапуска(СтрокаПараметров);
	
	ЮТест.ПроверитьРавенство(ПараметрыЗапуска.Выполнить, Истина, "ПараметрыЗапуска.Выполнить");
	ЮТест.ПроверитьРавенство(ПараметрыЗапуска.ПутьТестов, "E:\Путь с пробелом\Tests", "ПараметрыЗапуска.ПутьТестов");
	ЮТест.ПроверитьРавенство(ПараметрыЗапуска.ОстановитьСистему, Истина, "ПараметрыЗапуска.ОстановитьСистему");
	ЮТест.ПроверитьРавенство(ПараметрыЗапуска.ФорматОтчета, "xml", "ПараметрыЗапуска.ФорматОтчета");
	ЮТест.ПроверитьРавенство(ПараметрыЗапуска.ПутьОтчета, "E:\Путь с пробелом\testReport\", "ПараметрыЗапуска.ПутьОтчета");
	
	ЮТест.ПроверитьРавенство(ПараметрыЗапуска.Загрузить, Истина, "ПараметрыЗапуска.Загрузить");
	ЮТест.ПроверитьРавенство(ПараметрыЗапуска.ВстроенныеТесты, Ложь, "ПараметрыЗапуска.ВстроенныеТесты");
	ЮТест.ПроверитьРавенство(ПараметрыЗапуска.ПутьКодаВозврата, "E:\f1.log", "ПараметрыЗапуска.ПутьКодаВозврата");
КонецПроцедуры

Процедура ТестДолжен_ОбработатьВсеТиповыеПараметрыЗапуска_и_Запись_в_ЖР_АнглийскиеКоманды() Экспорт
	СтрокаПараметров = "xddRun;E:\Путь с пробелом\Tests;xddShutdown;xddReportFormat;xml;xddReportPath;E:\Путь с пробелом\testReport;xddExitCodePath;E:\f1.log;xddWriteLogEvent";
	
	ПараметрыЗапуска = РазобратьПараметрыЗапуска(СтрокаПараметров);
	
	ЮТест.ПроверитьРавенство(ПараметрыЗапуска.Выполнить, Истина, "ПараметрыЗапуска.Выполнить");
	ЮТест.ПроверитьРавенство(ПараметрыЗапуска.ПутьТестов, "E:\Путь с пробелом\Tests", "ПараметрыЗапуска.ПутьТестов");
	ЮТест.ПроверитьРавенство(ПараметрыЗапуска.ОстановитьСистему, Истина, "ПараметрыЗапуска.ОстановитьСистему");
	ЮТест.ПроверитьРавенство(ПараметрыЗапуска.ФорматОтчета, "xml", "ПараметрыЗапуска.ФорматОтчета");
	ЮТест.ПроверитьРавенство(ПараметрыЗапуска.ПутьОтчета, "E:\Путь с пробелом\testReport\", "ПараметрыЗапуска.ПутьОтчета");
	
	ЮТест.ПроверитьРавенство(ПараметрыЗапуска.Загрузить, Истина, "ПараметрыЗапуска.Загрузить");
	ЮТест.ПроверитьРавенство(ПараметрыЗапуска.ВстроенныеТесты, Ложь, "ПараметрыЗапуска.ВстроенныеТесты");
	ЮТест.ПроверитьРавенство(ПараметрыЗапуска.ПутьКодаВозврата, "E:\f1.log", "ПараметрыЗапуска.ПутьКодаВозврата");
	ЮТест.ПроверитьРавенство(ПараметрыЗапуска.РегистрацияПрохожденияТестовВЖР, Истина, "ПараметрыЗапуска.xddWriteLogEvent");
КонецПроцедуры

Процедура ТестДолжен_ОбработатьВсеТиповыеПараметрыЗапуска_ЕстьПробелыСпередиИСзадиКоманды() Экспорт
	СтрокаПараметров = " Тесты_Команда_Тестировать ; E:\Путь с пробелом\Tests ; ЗавершитьРаботуПослеТестирования ; Тесты_Команда_ФорматОтчета ; xml ; Тесты_Команда_КаталогОтчетов ; E:\Путь с пробелом\testReport ; Тесты_Путь_КЛогам ; E:\f1.log ";
	
	ПараметрыЗапуска = РазобратьПараметрыЗапуска(СтрокаПараметров);
	
	ЮТест.ПроверитьРавенство(ПараметрыЗапуска.Выполнить, Истина, "ПараметрыЗапуска.Выполнить");
	ЮТест.ПроверитьРавенство(ПараметрыЗапуска.ПутьТестов, "E:\Путь с пробелом\Tests", "ПараметрыЗапуска.ПутьТестов");
	ЮТест.ПроверитьРавенство(ПараметрыЗапуска.ОстановитьСистему, Истина, "ПараметрыЗапуска.ОстановитьСистему");
	ЮТест.ПроверитьРавенство(ПараметрыЗапуска.ФорматОтчета, "xml", "ПараметрыЗапуска.ФорматОтчета");
	ЮТест.ПроверитьРавенство(ПараметрыЗапуска.ПутьОтчета, "E:\Путь с пробелом\testReport\", "ПараметрыЗапуска.ПутьОтчета");
	
	ЮТест.ПроверитьРавенство(ПараметрыЗапуска.Загрузить, Истина, "ПараметрыЗапуска.Загрузить");
	ЮТест.ПроверитьРавенство(ПараметрыЗапуска.ВстроенныеТесты, Ложь, "ПараметрыЗапуска.ВстроенныеТесты");
	ЮТест.ПроверитьРавенство(ПараметрыЗапуска.ПутьКодаВозврата, "E:\f1.log", "ПараметрыЗапуска.ПутьКодаВозврата");
КонецПроцедуры

Процедура ТестДолжен_ОбработатьВсеТиповыеПараметрыЗапускаДляВстроенныхТестов() Экспорт
	СтрокаПараметров = "ЭтоТестИзКонфигурации;Тесты_Команда_Тестировать;Подсистема.Тестирование;ЗавершитьРаботуПослеТестирования;Тесты_Команда_ФорматОтчета;xml;Тесты_Команда_КаталогОтчетов;E:\Путь с пробелом\testReport;Тесты_Путь_КЛогам;E:\f1.log";
	
	ПараметрыЗапуска = РазобратьПараметрыЗапуска(СтрокаПараметров);
	
	ЮТест.ПроверитьРавенство(ПараметрыЗапуска.Выполнить, Истина, "ПараметрыЗапуска.Выполнить");
	ЮТест.ПроверитьРавенство(ПараметрыЗапуска.ПутьТестов, "Подсистема.Тестирование", "ПараметрыЗапуска.ПутьТестов");
	ЮТест.ПроверитьРавенство(ПараметрыЗапуска.ОстановитьСистему, Истина, "ПараметрыЗапуска.ОстановитьСистему");
	ЮТест.ПроверитьРавенство(ПараметрыЗапуска.ФорматОтчета, "xml", "ПараметрыЗапуска.ФорматОтчета");
	ЮТест.ПроверитьРавенство(ПараметрыЗапуска.ПутьОтчета, "E:\Путь с пробелом\testReport\", "ПараметрыЗапуска.ПутьОтчета");
	
	ЮТест.ПроверитьРавенство(ПараметрыЗапуска.Загрузить, Истина, "ПараметрыЗапуска.Загрузить");
	ЮТест.ПроверитьРавенство(ПараметрыЗапуска.ВстроенныеТесты, Истина, "ПараметрыЗапуска.ВстроенныеТесты");
	ЮТест.ПроверитьРавенство(ПараметрыЗапуска.ПутьКодаВозврата, "E:\f1.log", "ПараметрыЗапуска.ПутьКодаВозврата");
КонецПроцедуры

Процедура ТестДолжен_ОбработатьВсеТиповыеПараметрыЗапускаДляВстроенныхТестов_АнглийскиеКоманды() Экспорт
	СтрокаПараметров = "xddConfig;xddRun;Подсистема.Тестирование;xddShutdown;xddReportFormat;xml;xddReportPath;E:\Путь с пробелом\testReport;xddExitCodePath;E:\f1.log";
	
	ПараметрыЗапуска = РазобратьПараметрыЗапуска(СтрокаПараметров);
	
	ЮТест.ПроверитьРавенство(ПараметрыЗапуска.Выполнить, Истина, "ПараметрыЗапуска.Выполнить");
	ЮТест.ПроверитьРавенство(ПараметрыЗапуска.ПутьТестов, "Подсистема.Тестирование", "ПараметрыЗапуска.ПутьТестов");
	ЮТест.ПроверитьРавенство(ПараметрыЗапуска.ОстановитьСистему, Истина, "ПараметрыЗапуска.ОстановитьСистему");
	ЮТест.ПроверитьРавенство(ПараметрыЗапуска.ФорматОтчета, "xml", "ПараметрыЗапуска.ФорматОтчета");
	ЮТест.ПроверитьРавенство(ПараметрыЗапуска.ПутьОтчета, "E:\Путь с пробелом\testReport\", "ПараметрыЗапуска.ПутьОтчета");
	
	ЮТест.ПроверитьРавенство(ПараметрыЗапуска.Загрузить, Истина, "ПараметрыЗапуска.Загрузить");
	ЮТест.ПроверитьРавенство(ПараметрыЗапуска.ВстроенныеТесты, Истина, "ПараметрыЗапуска.ВстроенныеТесты");
	ЮТест.ПроверитьРавенство(ПараметрыЗапуска.ПутьКодаВозврата, "E:\f1.log", "ПараметрыЗапуска.ПутьКодаВозврата");
КонецПроцедуры

Процедура ВыполнитьОдинТестЧерезЗапуск1СПредприятия(Настройка) Экспорт
	ТолстыйКлиент = Настройка.ЗапускатьТолстыйКлиент;
	ИмяПользователя = Настройка.ИмяПользователя;
	ИмяМакета = Настройка.ИмяМакета;
	ПутьТестов = Настройка.ПутьТестов;
	ОжидаемоеКоличествоУпавшихТестов = Настройка.КоличествоУпавшихТестов;
	РежимЗапуска = Настройка.РежимЗапуска;
	
	СтрокаСоединения = СтрокаСоединенияИнформационнойБазы();		
	ПутьКФайловойБазе = НСтр(СтрокаСоединения, "File");
	
	Если НЕ ПустаяСтрока(ПутьКФайловойБазе) Тогда
		СтрокаСоединения = " /F " + ПутьКФайловойБазе;
	Иначе
		СтрокаСоединения = " /S " + НСтр(СтрокаСоединения, "Srvr") + "\" + НСтр(СтрокаСоединения, "Ref");
	КонецЕсли;
	СтрокаСоединения = СтрокаСоединения + " /N " + ИмяПользователя;
		
	СисИнфо = Новый СистемнаяИнформация;
	ВерсияПриложения = СисИнфо.ВерсияПриложения;
	
	Если Лев(ВерсияПриложения, 3) = "8.2" Тогда
		суффикс = "1cv82";
	ИначеЕсли Лев(ВерсияПриложения, 3) >= "8.3" Тогда
		суффикс = "1cv8";
	Иначе
		ВызватьИсключение "Версию приложению <"+ВерсияПриложения+"> обрабатывать не умеем";
	КонецЕсли;
	
	ПутьКПлатформе1С = "C:\Program Files (x86)\"+суффикс+"\"+ВерсияПриложения+"\bin\";
	Если ТолстыйКлиент Тогда
		ПутьКПлатформе1С = ПутьКПлатформе1С + "1cv8.exe";
	Иначе
		ПутьКПлатформе1С = ПутьКПлатформе1С + "1cv8c.exe";
	КонецЕсли;
	
	СтрокаРежимЗапуска = "";
	Если РежимЗапуска = РежимЗапускаКлиентскогоПриложения.УправляемоеПриложение Тогда
		СтрокаРежимЗапуска = " /RunModeManagedApplication ";
	ИначеЕсли РежимЗапуска = РежимЗапускаКлиентскогоПриложения.ОбычноеПриложение Тогда	
		СтрокаРежимЗапуска = " /RunModeOrdinary ";
	КонецЕсли;
	
	ИспользуемоеИмяФайлаЗапускателяТестов = ЮТест.ИспользуемоеИмяФайла;
	
	ПутьОтчетов = ПолучитьИмяВременногоФайла(); //+"-log";
	СоздатьКаталог(ПутьОтчетов);
	
	ПутьОтчетов = ПутьОтчетов + "\";
	ПутьФайлаОтчета1С = ПутьОтчетов+"1c.log";
	
	ПутьФайлаКодаВозврата = ПутьОтчетов+"exitCode.log";
	
	СтрокаПараметров = "xddRun;"+ПутьТестов+";xddShutdown;xddReportFormat;xml;xddReportPath;"+
		ПутьОтчетов+";xddExitCodePath;"+ПутьФайлаКодаВозврата+";xddWriteLogEvent";
		
	СтрокаКоманды = """"+ПутьКПлатформе1С + """ "+СтрокаРежимЗапуска + СтрокаСоединения +" /DisableStartupMessages /Execute "+ИспользуемоеИмяФайлаЗапускателяТестов+
		" /Out "+ПутьФайлаОтчета1С+" /C """+СтрокаПараметров+""" /LogUI ";
	
	ЗапуститьПриложение(СтрокаКоманды,, Истина);
	
	КоличествоУпавшихТестов = 0;
	ЧтениеXML = Новый ЧтениеXML;
	ЧтениеXML.ОткрытьФайл(ПутьОтчетов+"лог_тестирования.xml");
	Пока ЧтениеXML.Прочитать() Цикл
		Если ЧтениеXML.ТипУзла	= ТипУзлаXML.НачалоЭлемента и ЧтениеXML.Имя = "testsuites" Тогда
			Для сч = 0 По ЧтениеXML.КоличествоАтрибутов()-1 Цикл
				Если ЧтениеXML.ИмяАтрибута(сч) = "failures" Тогда
					КоличествоУпавшихТестов = Число(ЧтениеXML.ЗначениеАтрибута(сч));
				КонецЕсли;
			КонецЦикла;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	Если ОжидаемоеКоличествоУпавшихТестов <> 0 Тогда
		ЮТест.ПроверитьРавенство(ОжидаемоеКоличествоУпавшихТестов, КоличествоУпавшихТестов, "Ожидали, что упадут тесты ("+ОжидаемоеКоличествоУпавшихТестов+"шт), а остальные пройдут, а получили <"+КоличествоУпавшихТестов+"> упавших тестов.");
	Иначе
		ЮТест.ПроверитьРавенство(ОжидаемоеКоличествоУпавшихТестов, КоличествоУпавшихТестов, "Ожидали, что пройдут все тесты, а получили <"+КоличествоУпавшихТестов+"> упавших тестов.");
	КонецЕсли;
	
	ОжидаемыйКодВозврата = "0";
	ФайлКодаВозврата = Новый Файл(ПутьФайлаКодаВозврата);
	ЮТест.Проверить(ФайлКодаВозврата.Существует(), "Ожидали, что будет сформирован файл кода возврата, а файла нет");
	
	ЧтениеТекста = Новый ЧтениеТекста(ПутьФайлаКодаВозврата);
	ЮТест.ПроверитьРавенство(ОжидаемыйКодВозврата, ЧтениеТекста.ПрочитатьСтроку(), "Ожидали, что код возврата будет <"+ОжидаемыйКодВозврата+">, а получили другой код.");
КонецПроцедуры

Функция ПутьЗапускателяТестов()
	ПутьЗапускателяТестов = СтрЗаменить(ЮТест.ИспользуемоеИмяФайла, ИмяЗапускателяТестов() + ".epf", "");
	Возврат ПутьЗапускателяТестов;
КонецФункции

Функция ИмяЗапускателяТестов()
	Возврат "xddTestRunner";
КонецФункции

//} 

Функция РазобратьПараметрыЗапуска(СтрокаПараметровЗапуска) Экспорт
	Возврат ЮТест.РазобратьПараметрыЗапуска(СтрокаПараметровЗапуска);
КонецФункции
