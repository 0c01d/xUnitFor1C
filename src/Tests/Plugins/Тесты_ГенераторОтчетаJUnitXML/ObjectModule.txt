Перем КонтекстЯдра;
Перем Ожидаем;

Перем ФайлОтчета;

Процедура Инициализация(КонтекстЯдраПараметр) Экспорт
	КонтекстЯдра = КонтекстЯдраПараметр;
	Ожидаем = КонтекстЯдра.Плагин("УтвержденияBDD");
КонецПроцедуры

Функция ПолучитьСписокТестов() Экспорт
	ВсеТесты = Новый Массив;
	ВсеТесты.Добавить("ТестДолжен_СоздатьОтчет");
	ВсеТесты.Добавить("ТестДолжен_Экспортировать_ОтчетОТестировании");
	
	Возврат ВсеТесты;
КонецФункции

Процедура ПередЗапускомТеста() Экспорт
	
КонецПроцедуры

Процедура ПослеЗапускаТеста() Экспорт
	Если ФайлОтчета <> Неопределено Тогда
		УдалитьФайлы(ФайлОтчета.ПолноеИмя);
	КонецЕсли;
КонецПроцедуры

Процедура ТестДолжен_СоздатьОтчет() Экспорт
	РезультатТестирования = ПодготовитьРезультатТестирования();
	
	ГенераторОтчета = КонтекстЯдра.Плагин("ГенераторОтчетаJUnitXML");
	Отчет = ГенераторОтчета.СоздатьОтчет(КонтекстЯдра, РезультатТестирования);
	
	Ожидаем.Что(Отчет).ИмеетТип("ТекстовыйДокумент");
	ТекстОтчета = УбратьИзСтрокиТабуляцииИПереносыСтрок(Отчет.ПолучитьТекст());
	
	Эталон_ОтчетОТестировании = ПолучитьМакет("Эталон_ОтчетОТестировании");
	ТекстЭталона = УбратьИзСтрокиТабуляцииИПереносыСтрок(Отчет.ПолучитьТекст());
	
	Ожидаем.Что(ТекстОтчета).Равно(ТекстЭталона);
КонецПроцедуры

Функция ПодготовитьРезультатТестирования()
	ПостроительДереваТестов = КонтекстЯдра.Плагин("ПостроительДереваТестов");
	УспешныйЭлемент = ПостроительДереваТестов.СоздатьЭлемент(ЭтотОбъект.ИспользуемоеИмяФайла, "УспешныйМетод");
	ПадающийЭлемент = ПостроительДереваТестов.СоздатьЭлемент(ЭтотОбъект.ИспользуемоеИмяФайла, "ПадающийМетод");
	ОтсутствующийЭлемент = ПостроительДереваТестов.СоздатьЭлемент(ЭтотОбъект.ИспользуемоеИмяФайла, "ОтсутствующийМетод");
	
	Контейнер = ПостроительДереваТестов.СоздатьКонтейнер("Контейнер");
	Контейнер.Строки.Добавить(УспешныйЭлемент);
	Контейнер.Строки.Добавить(ПадающийЭлемент);
	Контейнер.Строки.Добавить(ОтсутствующийЭлемент);
	
	РезультатТестирования = КонтекстЯдра.ВыполнитьТесты(ЗагрузчикЗаглушка(), Контейнер);
	
	Возврат РезультатТестирования;
КонецФункции

Функция УбратьИзСтрокиТабуляцииИПереносыСтрок(Знач Результат)
	Результат = СтрЗаменить(Результат, Символы.Таб, "");
	Результат = СтрЗаменить(Результат, Символы.ПС, "");
	
	Возврат Результат;
КонецФункции

Процедура ТестДолжен_Экспортировать_ОтчетОТестировании() Экспорт
	РезультатТестирования = ПодготовитьРезультатТестирования();
	
	ГенераторОтчета = КонтекстЯдра.Плагин("ГенераторОтчетаJUnitXML");
	Отчет = ГенераторОтчета.СоздатьОтчет(КонтекстЯдра, РезультатТестирования);
	
	ИмяФайла = ПолучитьИмяВременногоФайла("xml");
	ФайлОтчета = Новый Файл(ИмяФайла);
	ГенераторОтчета.Экспортировать(Отчет, ФайлОтчета.ПолноеИмя);
	
	Ожидаем.Что(ФайлОтчета.Существует()).ЭтоИстина();
	
	ТекстовыйДокумент = Новый ТекстовыйДокумент;
	ТекстовыйДокумент.Прочитать(ФайлОтчета.ПолноеИмя);
	ТекстОтчета = УбратьИзСтрокиТабуляцииИПереносыСтрок(ТекстовыйДокумент.ПолучитьТекст());
	
	Эталон_ОтчетОТестировании = ПолучитьМакет("Эталон_ОтчетОТестировании");
	ТекстЭталона = УбратьИзСтрокиТабуляцииИПереносыСтрок(Отчет.ПолучитьТекст());
	
	Ожидаем.Что(ТекстОтчета).Равно(ТекстЭталона);
КонецПроцедуры

// Методы нужные для тестов
Функция ЗагрузчикЗаглушка()
	Возврат ЭтотОбъект;
КонецФункции

Функция ПолучитьКонтекстПоПути(КонтекстЯдра, Путь) Экспорт
	Возврат ЭтотОбъект;
КонецФункции

Процедура УспешныйМетод() Экспорт
КонецПроцедуры

Процедура ПадающийМетод() Экспорт
	ВызватьИсключение "БАБАХ!!!";
КонецПроцедуры
