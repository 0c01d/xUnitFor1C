Перем КонтекстЯдра;
Перем Ожидаем;

Процедура Инициализация(КонтекстЯдраПараметр) Экспорт
	КонтекстЯдра = КонтекстЯдраПараметр;
	Ожидаем = КонтекстЯдра.Плагин("УтвержденияBDDCollection");
КонецПроцедуры

Функция ПолучитьСписокТестов() Экспорт
	ВсеТесты = Новый Массив;
	ВсеТесты.Добавить("ТестДолжен_Проверить_ЭтоИстина");
	ВсеТесты.Добавить("ТестДолжен_Проверить_Отрицание_ЭтоИстина");
	ВсеТесты.Добавить("ТестДолжен_Проверить_ЭтоЛожь");
	ВсеТесты.Добавить("ТестДолжен_Проверить_Отрицание_ЭтоЛожь");
	ВсеТесты.Добавить("ТестДолжен_Проверить_Равно");
	ВсеТесты.Добавить("ТестДолжен_Проверить_Отрицание_Равно");
	ВсеТесты.Добавить("ТестДолжен_Проверить_Больше");
	ВсеТесты.Добавить("ТестДолжен_Проверить_Отрицание_Больше");
	ВсеТесты.Добавить("ТестДолжен_Проверить_БольшеИлиРавно_И_Минимум");
	ВсеТесты.Добавить("ТестДолжен_Проверить_Отрицание_БольшеИлиРавно_И_Минимум");
	ВсеТесты.Добавить("ТестДолжен_Проверить_МеньшеИлиРавно_И_Максимум");
	ВсеТесты.Добавить("ТестДолжен_Проверить_Отрицание_МеньшеИлиРавно_И_Максимум");
	ВсеТесты.Добавить("ТестДолжен_Проверить_Меньше");
	ВсеТесты.Добавить("ТестДолжен_Проверить_Отрицание_Меньше");
	ВсеТесты.Добавить("ТестДолжен_Проверить_Заполнено");
	ВсеТесты.Добавить("ТестДолжен_Проверить_Отрицание_Заполнено");
	ВсеТесты.Добавить("ТестДолжен_Проверить_Существует");
	ВсеТесты.Добавить("ТестДолжен_Проверить_Отрицание_Существует");
	ВсеТесты.Добавить("ТестДолжен_Проверить_ЭтоНеопределено");
	ВсеТесты.Добавить("ТестДолжен_Проверить_Отрицание_ЭтоНеопределено");
	ВсеТесты.Добавить("ТестДолжен_Проверить_ЭтоNull");
	ВсеТесты.Добавить("ТестДолжен_Проверить_Отрицание_ЭтоNull");
	ВсеТесты.Добавить("ТестДолжен_Проверить_ИмеетТип");
	ВсеТесты.Добавить("ТестДолжен_Проверить_Отрицание_ИмеетТип");
	ВсеТесты.Добавить("ТестДолжен_Проверить_Между");
	ВсеТесты.Добавить("ТестДолжен_Проверить_Отрицание_Между");
	ВсеТесты.Добавить("ТестДолжен_Проверить_Содержит");
	ВсеТесты.Добавить("ТестДолжен_Проверить_Отрицание_Содержит");
	ВсеТесты.Добавить("ТестДолжен_Проверить_ИмеетДлину");
	ВсеТесты.Добавить("ТестДолжен_Проверить_Отрицание_ИмеетДлину");
	ВсеТесты.Добавить("ТестДолжен_Проверить_КомпозитныеУтверждения");
	ВсеТесты.Добавить("ТестДолжен_Проверить_КомпозитныеУтверждения_ВПеремешкуС_Отрицаниями");
	ВсеТесты.Добавить("ТестДолжен_Проверить_ВыбрасываетИсключение");
	ВсеТесты.Добавить("ТестДолжен_Проверить_Отрицание_ВыбрасываетИсключение");
	ВсеТесты.Добавить("ТестДолжен_Проверить_ВыбрасываемыеСообщения");
	ВсеТесты.Добавить("ТестДолжен_Проверить_ЭтоНе");
	ВсеТесты.Добавить("ТестДолжен_Проверить_ЕстьИстина");
	ВсеТесты.Добавить("ТестДолжен_Проверить_ЕстьЛожь");
	ВсеТесты.Добавить("ТестДолжен_Проверить_ЕстьНеопределено");
	ВсеТесты.Добавить("ТестДолжен_Проверить_ЕстьNull");
	
	ВсеТесты.Добавить("ТестДолжен_Проверить_ЧислаРавны");
	ВсеТесты.Добавить("ТестДолжен_Проверить_ЧислаРазличаются");
    
	ВсеТесты.Добавить("ТестДолжен_Проверить_СтрокиРавны");
	ВсеТесты.Добавить("ТестДолжен_Проверить_СтрокиРазличаются");
    
	ВсеТесты.Добавить("ТестДолжен_Проверить_СтруктурыРавны");
	ВсеТесты.Добавить("ТестДолжен_Проверить_СтруктурыРазличаются");
	ВсеТесты.Добавить("ТестДолжен_Проверить_СтруктурыРавныЧерезСтарыйИнтерфейс");

	ВсеТесты.Добавить("ТестДолжен_Проверить_МассивыРавны");
	ВсеТесты.Добавить("ТестДолжен_Проверить_МассивыРазличаются");
	ВсеТесты.Добавить("ТестДолжен_Проверить_МассивыРавныЧерезСтарыйИнтерфейс");

	ВсеТесты.Добавить("ТестДолжен_Проверить_СЗРавны");
	ВсеТесты.Добавить("ТестДолжен_Проверить_СЗРазличаются");
	ВсеТесты.Добавить("ТестДолжен_Проверить_СЗРавныЧерезСтарыйИнтерфейс");
    
    
	ВсеТесты.Добавить("ТестДолжен_Проверить_СоответствияРавны");
	ВсеТесты.Добавить("ТестДолжен_Проверить_СоответствияРазличаются");
	ВсеТесты.Добавить("ТестДолжен_Проверить_СоответствияРавныЧерезСтарыйИнтерфейс");
    
	ВсеТесты.Добавить("ТестДолжен_Проверить_ТЗРавны");
	ВсеТесты.Добавить("ТестДолжен_Проверить_ТЗРазличаются");
	ВсеТесты.Добавить("ТестДолжен_Проверить_ТЗРавныЧерезСтарыйИнтерфейс");
    
	ВсеТесты.Добавить("ТестДолжен_Проверить_ДЗРавны");
	ВсеТесты.Добавить("ТестДолжен_Проверить_ДЗРазличаются");
	ВсеТесты.Добавить("ТестДолжен_Проверить_ДЗРавныЧерезСтарыйИнтерфейс");
    
    Возврат ВсеТесты;
КонецФункции

Процедура ТестДолжен_Проверить_ЭтоИстина() Экспорт
	Ожидаем.Что(5 > 1).ЭтоИстина();
	Ожидаем.Что(Истина).ЭтоИстина();
КонецПроцедуры

Процедура ТестДолжен_Проверить_Отрицание_ЭтоИстина() Экспорт
	Ожидаем.Что(5 < 1).Не_().ЭтоИстина();
	Ожидаем.Что(Ложь).Не_().ЭтоИстина();
	Ожидаем.Что(Неопределено).Не_().ЭтоИстина();
КонецПроцедуры

Процедура ТестДолжен_Проверить_ЭтоЛожь() Экспорт
	Ожидаем.Что(5 < 1).ЭтоЛожь();
	Ожидаем.Что(Ложь).ЭтоЛожь();
КонецПроцедуры

Процедура ТестДолжен_Проверить_Отрицание_ЭтоЛожь() Экспорт
	Ожидаем.Что(5 > 1).Не_().ЭтоЛожь();
	Ожидаем.Что(Истина).Не_().ЭтоЛожь();
	Ожидаем.Что(Неопределено).Не_().ЭтоЛожь();
КонецПроцедуры

Процедура ТестДолжен_Проверить_Равно() Экспорт
	Ожидаем.Что(5).Равно(5);
	Ожидаем.Что("некая строка").Равно("некая строка");
КонецПроцедуры

Процедура ТестДолжен_Проверить_Отрицание_Равно() Экспорт
	Ожидаем.Что(5).Не_().Равно(7);
КонецПроцедуры

Процедура ТестДолжен_Проверить_Больше() Экспорт
	Ожидаем.Что(4).Больше(2);
КонецПроцедуры

Процедура ТестДолжен_Проверить_Отрицание_Больше() Экспорт
	Ожидаем.Что(7).Не_().Больше(11);
	Ожидаем.Что(7).Не_().Больше(7);
КонецПроцедуры

Процедура ТестДолжен_Проверить_БольшеИлиРавно_И_Минимум() Экспорт
	НекоеЧисло = 9;
	Ожидаем.Что(НекоеЧисло).БольшеИлиРавно(5);
	Ожидаем.Что(НекоеЧисло).БольшеИлиРавно(9);
	
	Ожидаем.Что(НекоеЧисло).Минимум(1);
	Ожидаем.Что(НекоеЧисло).Минимум(9);
КонецПроцедуры

Процедура ТестДолжен_Проверить_Отрицание_БольшеИлиРавно_И_Минимум() Экспорт
	НекоеЧисло = 15;
	Ожидаем.Что(НекоеЧисло).Не_().БольшеИлиРавно(20);
	Ожидаем.Что(НекоеЧисло).Не_().Минимум(16);
КонецПроцедуры

Процедура ТестДолжен_Проверить_МеньшеИлиРавно_И_Максимум() Экспорт
	НекоеЧисло = 30;
	Ожидаем.Что(НекоеЧисло).МеньшеИлиРавно(32);
	Ожидаем.Что(НекоеЧисло).МеньшеИлиРавно(30);
	
	Ожидаем.Что(НекоеЧисло).Максимум(37);
	Ожидаем.Что(НекоеЧисло).Максимум(30);
КонецПроцедуры

Процедура ТестДолжен_Проверить_Отрицание_МеньшеИлиРавно_И_Максимум() Экспорт
	НекоеЧисло = 17;
	Ожидаем.Что(НекоеЧисло).Не_().МеньшеИлиРавно(16);
	Ожидаем.Что(НекоеЧисло).Не_().Максимум(11);
КонецПроцедуры

Процедура ТестДолжен_Проверить_Меньше() Экспорт
	Ожидаем.Что(5).Меньше(10);
КонецПроцедуры

Процедура ТестДолжен_Проверить_Отрицание_Меньше() Экспорт
	Ожидаем.Что(5).Не_().Меньше(3);
	Ожидаем.Что(5).Не_().Меньше(5);
КонецПроцедуры

Процедура ТестДолжен_Проверить_Заполнено() Экспорт
	Ожидаем.Что(7).Заполнено();
	Ожидаем.Что("!!!").Заполнено();
КонецПроцедуры

Процедура ТестДолжен_Проверить_Отрицание_Заполнено() Экспорт
	Ожидаем.Что(0).Не_().Заполнено();
	Ожидаем.Что("").Не_().Заполнено();
	Ожидаем.Что(Новый Массив).Не_().Заполнено();
КонецПроцедуры

Процедура ТестДолжен_Проверить_Существует() Экспорт
	Ожидаем.Что(0).Существует();
	Ожидаем.Что("").Существует();
	Ожидаем.Что(Новый Массив).Существует();
КонецПроцедуры

Процедура ТестДолжен_Проверить_Отрицание_Существует() Экспорт
	Ожидаем.Что(Неопределено).Не_().Существует();
	Ожидаем.Что(Null).Не_().Существует();
КонецПроцедуры

Процедура ТестДолжен_Проверить_ЭтоНеопределено() Экспорт
	Перем Переменная;
	Ожидаем.Что(Переменная).ЭтоНеопределено();
КонецПроцедуры

Процедура ТестДолжен_Проверить_Отрицание_ЭтоНеопределено() Экспорт
	Ожидаем.Что(0).Не_().ЭтоНеопределено();
	Ожидаем.Что(Null).Не_().ЭтоНеопределено();
КонецПроцедуры

Процедура ТестДолжен_Проверить_ЭтоNull() Экспорт
	Ссылка = Null;
	Ожидаем.Что(Ссылка).ЭтоNull();
КонецПроцедуры

Процедура ТестДолжен_Проверить_Отрицание_ЭтоNull() Экспорт
	Ожидаем.Что("").Не_().ЭтоNull();
	Ожидаем.Что(Неопределено).Не_().ЭтоNull();
КонецПроцедуры

Процедура ТестДолжен_Проверить_ИмеетТип() Экспорт
	Ожидаем.Что(Новый Массив).ИмеетТип("Массив");
	Ожидаем.Что(5).ИмеетТип(Тип("Число"));
КонецПроцедуры

Процедура ТестДолжен_Проверить_Отрицание_ИмеетТип() Экспорт
	Ожидаем.Что("").Не_().ИмеетТип("Массив");
	Ожидаем.Что(5).Не_().ИмеетТип(Тип("Дата"));
КонецПроцедуры

Процедура ТестДолжен_Проверить_Между() Экспорт
	Ожидаем.Что(5).Между(3, 7);
	Ожидаем.Что(3).Между(3, 7);
	Ожидаем.Что(7).Между(3, 7);
КонецПроцедуры

Процедура ТестДолжен_Проверить_Отрицание_Между() Экспорт
	Ожидаем.Что(1).Не_().Между(3, 7);
КонецПроцедуры

Процедура ТестДолжен_Проверить_Содержит() Экспорт
	Ожидаем.Что("Некая строка", "Строка").Содержит("ока");
	
	НекийМассив = Новый Массив;
	НекийМассив.Добавить(1);
	НекийМассив.Добавить(2);
	Ожидаем.Что(НекийМассив, "НекийМассив").Содержит(2);
	
	НекийФиксированныйМассив = Новый ФиксированныйМассив(НекийМассив);
	Ожидаем.Что(НекийМассив, "НекийФиксированныйМассив").Содержит(1);
	
	НекаяСтруктура = Новый Структура("Ключ1, Ключ2", "Значение1", "Значение2");
	Ожидаем.Что(НекаяСтруктура, "НекаяСтруктура").Содержит("Значение1");
	
	НекаяФиксированнаяСтруктура = Новый ФиксированнаяСтруктура(НекаяСтруктура);
	Ожидаем.Что(НекаяФиксированнаяСтруктура, "НекаяФиксированнаяСтруктура").Содержит("Значение2");
	
	НекоеСоответствие = Новый Соответствие;
	НекоеСоответствие.Вставить(1, НекийМассив);
	НекоеСоответствие.Вставить(2, НекаяСтруктура);
	Ожидаем.Что(НекоеСоответствие, "НекоеСоответствие").Содержит(НекийМассив);
	
	НекоеФиксированноеСоответствие = Новый ФиксированноеСоответствие(НекоеСоответствие);
	Ожидаем.Что(НекоеФиксированноеСоответствие, "НекоеФиксированноеСоответствие").Содержит(НекаяСтруктура);
	
	НекийСписокЗначений = Новый СписокЗначений;
	НекийСписокЗначений.Добавить("знач1");
	НекийСписокЗначений.Добавить("знач2");
	Ожидаем.Что(НекийСписокЗначений, "НекийСписокЗначений").Содержит("знач1");
КонецПроцедуры

Процедура ТестДолжен_Проверить_Отрицание_Содержит() Экспорт
	Ожидаем.Что("Некая строка").Не_().Содержит("!!!");
	
	НекийМассив = Новый Массив;
	НекийМассив.Добавить(1);
	НекийМассив.Добавить(2);
	Ожидаем.Что(НекийМассив).Не_().Содержит(7);
КонецПроцедуры

Процедура ТестДолжен_Проверить_ИмеетДлину() Экспорт
	Ожидаем.Что("Некая строка", "Строка").ИмеетДлину(12);
	
	НекийМассив = Новый Массив;
	НекийМассив.Добавить(1);
	НекийМассив.Добавить(2);
	Ожидаем.Что(НекийМассив, "НекийМассив").ИмеетДлину(2);
	
	НекийФиксированныйМассив = Новый ФиксированныйМассив(НекийМассив);
	Ожидаем.Что(НекийМассив, "НекийФиксированныйМассив").ИмеетДлину(2);
	
	НекаяСтруктура = Новый Структура("Ключ1, Ключ2, Ключ3", "Значение1", "Значение2");
	Ожидаем.Что(НекаяСтруктура, "НекаяСтруктура").ИмеетДлину(3);
	
	НекаяФиксированнаяСтруктура = Новый ФиксированнаяСтруктура(НекаяСтруктура);
	Ожидаем.Что(НекаяФиксированнаяСтруктура, "НекаяФиксированнаяСтруктура").ИмеетДлину(3);
	
	НекоеСоответствие = Новый Соответствие;
	НекоеСоответствие.Вставить(1, НекийМассив);
	НекоеСоответствие.Вставить(2, НекаяСтруктура);
	Ожидаем.Что(НекоеСоответствие, "НекоеСоответствие").ИмеетДлину(2);
	
	НекоеФиксированноеСоответствие = Новый ФиксированноеСоответствие(НекоеСоответствие);
	Ожидаем.Что(НекоеФиксированноеСоответствие, "НекоеФиксированноеСоответствие").ИмеетДлину(2);
	
	НекийСписокЗначений = Новый СписокЗначений;
	НекийСписокЗначений.ЗагрузитьЗначения(НекийМассив);
	НекийСписокЗначений.Добавить("знач1");
	НекийСписокЗначений.Добавить("знач2");
	Ожидаем.Что(НекийСписокЗначений, "НекийСписокЗначений").ИмеетДлину(4);
КонецПроцедуры

Процедура ТестДолжен_Проверить_Отрицание_ИмеетДлину() Экспорт
	Ожидаем.Что("Некая строка", "Строка").Не_().ИмеетДлину(1);
	
	НекаяСтруктура = Новый Структура("Ключ1, Ключ2, Ключ3", "Значение1", "Значение2");
	Ожидаем.Что(НекаяСтруктура, "НекаяСтруктура").Не_().ИмеетДлину(4);
КонецПроцедуры

Процедура ТестДолжен_Проверить_КомпозитныеУтверждения() Экспорт
	Ожидаем.Что("Некая строка")
		.Существует()
		.ИмеетТип("Строка")
		.ИмеетДлину(12)
		.Содержит("стр");
КонецПроцедуры

Процедура ТестДолжен_Проверить_КомпозитныеУтверждения_ВПеремешкуС_Отрицаниями() Экспорт
	Ожидаем.Что("Некая строка")
		.Существует()
		.Не_().ИмеетТип("Число")
		.ИмеетДлину(12)
		.Не_().Содержит("!!!");
КонецПроцедуры

Процедура ТестДолжен_Проверить_ВыбрасываетИсключение() Экспорт
	Контекст = Новый Структура;
	
	Ожидаем.Что(Контекст).Метод("Вставить").ВыбрасываетИсключение();
	Ожидаем.Что(Контекст).Метод("Вставить").ВыбрасываетИсключение("Недостаточно фактических параметров");
	
	Параметры = Новый Массив;
	Параметры.Добавить("Плохой Ключ");
	Параметры.Добавить(5);
	Ожидаем.Что(Контекст).Метод("Вставить", Параметры).ВыбрасываетИсключение("Задано неправильное имя атрибута структуры");
КонецПроцедуры

Процедура ТестДолжен_Проверить_Отрицание_ВыбрасываетИсключение() Экспорт
	Контекст = Новый Структура;
	Параметры = Новый Массив;
	Параметры.Добавить("Ключ");
	Параметры.Добавить("Значение");
	Ожидаем.Что(Контекст).Метод("Вставить", Параметры).Не_().ВыбрасываетИсключение();
КонецПроцедуры

Процедура ТестДолжен_Проверить_ВыбрасываемыеСообщения() Экспорт
	ТекстИсключения = "";
	Попытка
		Ожидаем.Что(Ложь).ЭтоИстина();
	Исключение
		ТекстИсключения = ОписаниеОшибки();
	КонецПопытки;
	Ожидаем.Что(ТекстИсключения).Не_().Содержит("НЕ");
	
	ТекстИсключения = "";
	Попытка
		Ожидаем.Что(Истина).Не_().ЭтоИстина();
	Исключение
		ТекстИсключения = ОписаниеОшибки();
	КонецПопытки;
	Ожидаем.Что(ТекстИсключения).Содержит("НЕ");
КонецПроцедуры

Процедура ТестДолжен_Проверить_ЭтоНе() Экспорт
	Ожидаем.Что(5).ЭтоНе().Равно(7);
КонецПроцедуры

Процедура ТестДолжен_Проверить_ЕстьИстина() Экспорт
	Ожидаем.Что(5 > 1).ЕстьИстина().Существует();
КонецПроцедуры

Процедура ТестДолжен_Проверить_ЕстьЛожь() Экспорт
	Ожидаем.Что(4 > 6).ЕстьЛожь().Существует();
КонецПроцедуры

Процедура ТестДолжен_Проверить_ЕстьНеопределено() Экспорт
	Ожидаем.Что(Неопределено).ЕстьНеопределено().ЭтоНе().Существует();
КонецПроцедуры

Процедура ТестДолжен_Проверить_ЕстьNull() Экспорт
	Ожидаем.Что(Null).ЕстьNull().ЭтоНе().Существует();
КонецПроцедуры

/// 
// { Выполнение тестов
Процедура ТестДолжен_Проверить_ЧислаРавны() Экспорт
    РезультатСравнения = Ожидаем.Проверить2Значения(2,2);
    Ожидаем.Что(РезультатСравнения).ИмеетТип("Структура");
    Ожидаем.Что(РезультатСравнения.Тип).Равно(1);
    Ожидаем.Что(РезультатСравнения.Результат).Равно(Истина);
КонецПроцедуры    
Процедура ТестДолжен_Проверить_ЧислаРазличаются() Экспорт
    РезультатСравнения = Ожидаем.Проверить2Значения(2,3);
    Ожидаем.Что(РезультатСравнения).ИмеетТип("Структура");
    Ожидаем.Что(РезультатСравнения.Тип).Равно(0);
    Ожидаем.Что(РезультатСравнения.Результат).Равно("Ожидали, что проверяемое значение (2) РАВНО (3)");
КонецПроцедуры    
Процедура ТестДолжен_Проверить_СтрокиРавны() Экспорт
    РезультатСравнения = Ожидаем.Проверить2Значения("ф","ф");
    Ожидаем.Что(РезультатСравнения).ИмеетТип("Структура");
    Ожидаем.Что(РезультатСравнения.Тип).Равно(1);
    Ожидаем.Что(РезультатСравнения.Результат).Равно(Истина);
КонецПроцедуры    
Процедура ТестДолжен_Проверить_СтрокиРазличаются() Экспорт
    РезультатСравнения = Ожидаем.Проверить2Значения("ф","фф");
    Ожидаем.Что(РезультатСравнения).ИмеетТип("Структура");
    Ожидаем.Что(РезультатСравнения.Тип).Равно(0);
    Ожидаем.Что(РезультатСравнения.Результат).Равно("Ожидали, что проверяемое значение (ф) РАВНО (фф)");
КонецПроцедуры    

Процедура ТестДолжен_Проверить_СтруктурыРавны() Экспорт
    Струткура1 = новый Структура("Поле1,Поле2",1,новый Структура("Данные",2));
    Струткура2 = новый Структура("Поле1,Поле2",1,новый Структура("Данные",2));
    РезультатСравнения = Ожидаем.Проверить2Значения(Струткура1,Струткура2);
    Ожидаем.Что(РезультатСравнения).ИмеетТип("Структура");
    Ожидаем.Что(РезультатСравнения.Тип).Равно(1);
    Ожидаем.Что(РезультатСравнения.Результат).Равно(Истина);
КонецПроцедуры    
Процедура ТестДолжен_Проверить_СтруктурыРазличаются() Экспорт
    Струткура1 = новый Структура("Поле1,Поле2",1,новый Структура("Данные",3));
    Струткура2 = новый Структура("Поле1,Поле2",1,новый Структура("Данные",2));
    РезультатСравнения = Ожидаем.Проверить2Значения(Струткура1,Струткура2);
    Ожидаем.Что(РезультатСравнения).ИмеетТип("Структура");
    Ожидаем.Что(РезультатСравнения.Тип).Равно(0);
    Ожидаем.Что(РезультатСравнения.Результат).Равно(
"Для ключа Поле2 ожидается 'Структура', передано 'Структура' 
|Для ключа Данные ожидается '3', передано '2' 
|Ожидали, что проверяемое значение (3) РАВНО (2)");
КонецПроцедуры    
Процедура ТестДолжен_Проверить_СтруктурыРавныЧерезСтарыйИнтерфейс() Экспорт
    Струткура1 = новый Структура("Поле1,Поле2",1,новый Структура("Данные",2));
    Струткура2 = новый Структура("Поле1,Поле2",1,новый Структура("Данные",2));
    Ожидаем.Что(Струткура1).Равно(Струткура2);
КонецПроцедуры    

Процедура ТестДолжен_Проверить_МассивыРавны() Экспорт
    Массив1 = новый массив;
    Массив1.Добавить(1);
    Массив1.Добавить("1");
    Массив1.Добавить(Неопределено);
    
    Массив2 = новый массив;
    Массив2.Добавить(1);
    Массив2.Добавить("1");
    Массив2.Добавить(Неопределено);
    
    РезультатСравнения = Ожидаем.Проверить2Значения(Массив1,Массив2);
    Ожидаем.Что(РезультатСравнения).ИмеетТип("Структура");
    Ожидаем.Что(РезультатСравнения.Тип).Равно(1);
    Ожидаем.Что(РезультатСравнения.Результат).Равно(Истина);
КонецПроцедуры    
Процедура ТестДолжен_Проверить_МассивыРазличаются() Экспорт
    Массив1 = новый массив;
    Массив1.Добавить(2);
    Массив1.Добавить("3");
    Массив1.Добавить(Неопределено);
    
    Массив2 = новый массив;
    Массив2.Добавить(1);
    Массив2.Добавить("1");
    Массив2.Добавить(Неопределено);
    
    РезультатСравнения = Ожидаем.Проверить2Значения(Массив1,Массив2);
    Ожидаем.Что(РезультатСравнения).ИмеетТип("Структура");
    Ожидаем.Что(РезультатСравнения.Тип).Равно(0);
    Ожидаем.Что(РезультатСравнения.Результат).Равно("Для строки 0 ожидается '2', передано '1' ");
КонецПроцедуры    
Процедура ТестДолжен_Проверить_МассивыРавныЧерезСтарыйИнтерфейс() Экспорт
    Массив1 = новый массив;
    Массив1.Добавить(2);
    Массив1.Добавить("3");
    Массив1.Добавить(Неопределено);
    
    Массив2 = новый массив;
    Массив2.Добавить(2);
    Массив2.Добавить("3");
    Массив2.Добавить(Неопределено);

    Ожидаем.Что(Массив1).Равно(Массив2);
КонецПроцедуры    

Процедура ТестДолжен_Проверить_СЗРавны() Экспорт

    СЗ1 = новый СписокЗначений;
    СЗ1.Добавить(1,"Один");
    СЗ1.Добавить(2,"Два");
    
    СЗ2 = новый СписокЗначений;
    СЗ2.Добавить(1,"Один");
    СЗ2.Добавить(2,"Два");
    
    РезультатСравнения = Ожидаем.Проверить2Значения(СЗ1,СЗ2);
    Ожидаем.Что(РезультатСравнения).ИмеетТип("Структура");
    Ожидаем.Что(РезультатСравнения.Тип).Равно(1);
    Ожидаем.Что(РезультатСравнения.Результат).Равно(Истина);
КонецПроцедуры    
Процедура ТестДолжен_Проверить_СЗРазличаются() Экспорт
    СЗ1 = новый СписокЗначений;
    СЗ1.Добавить(1,"Один");
    СЗ1.Добавить(2,"Два");
    
    СЗ2 = новый СписокЗначений;
    СЗ2.Добавить(1,"Один");
    СЗ2.Добавить(3,"Два");
    
    РезультатСравнения = Ожидаем.Проверить2Значения(СЗ1,СЗ2);
    Ожидаем.Что(РезультатСравнения).ИмеетТип("Структура");
    Ожидаем.Что(РезультатСравнения.Тип).Равно(0);
    Ожидаем.Что(РезультатСравнения.Результат).Равно("Значения различаются .  Ожидается '2', передано '3'");
КонецПроцедуры    
Процедура ТестДолжен_Проверить_СЗРавныЧерезСтарыйИнтерфейс() Экспорт
    СЗ1 = новый СписокЗначений;
    СЗ1.Добавить(1,"Один");
    СЗ1.Добавить(2,"Два");
    
    СЗ2 = новый СписокЗначений;
    СЗ2.Добавить(1,"Один");
    СЗ2.Добавить(2,"Два");

    Ожидаем.Что(СЗ1).Равно(СЗ2);
КонецПроцедуры    

Процедура ТестДолжен_Проверить_СоответствияРавны() Экспорт

    СЗ1 = новый Соответствие;
    СЗ1.Вставить(1,"Один");
    СЗ1.Вставить(2,"Два");
    
    СЗ2 = новый Соответствие;
    СЗ2.Вставить(1,"Один");
    СЗ2.Вставить(2,"Два");
    
    РезультатСравнения = Ожидаем.Проверить2Значения(СЗ1,СЗ2);
    Ожидаем.Что(РезультатСравнения).ИмеетТип("Структура");
    Ожидаем.Что(РезультатСравнения.Тип).Равно(1);
    Ожидаем.Что(РезультатСравнения.Результат).Равно(Истина);
КонецПроцедуры    
Процедура ТестДолжен_Проверить_СоответствияРазличаются() Экспорт
    СЗ1 = новый Соответствие;
    СЗ1.Вставить(1,"Один");
    СЗ1.Вставить(2,"Два");
    
    СЗ2 = новый Соответствие;
    СЗ2.Вставить(1,"Один");
    СЗ2.Вставить(3,"Два");
    
    РезультатСравнения = Ожидаем.Проверить2Значения(СЗ1,СЗ2);
    Ожидаем.Что(РезультатСравнения).ИмеетТип("Структура");
    Ожидаем.Что(РезультатСравнения.Тип).Равно(0);
    Ожидаем.Что(РезультатСравнения.Результат).Равно("Для ключа 2 ожидается 'Два', передано '' ");
КонецПроцедуры    
Процедура ТестДолжен_Проверить_СоответствияРавныЧерезСтарыйИнтерфейс() Экспорт

    СЗ1 = новый Соответствие;
    СЗ1.Вставить(1,"Один");
    СЗ1.Вставить(2,"Два");
    
    СЗ2 = новый Соответствие;
    СЗ2.Вставить(1,"Один");
    СЗ2.Вставить(2,"Два");

    Ожидаем.Что(СЗ1).Равно(СЗ2);
КонецПроцедуры    

Процедура ТестДолжен_Проверить_ТЗРавны() Экспорт

    ТЗ1 = новый ТаблицаЗначений;
    ТЗ1.Колонки.Добавить("А");
    ТЗ1.Колонки.Добавить("Б");
    
    Строка = ТЗ1.Добавить();
    Строка.А = 1; Строка.Б= 1;
    Строка = ТЗ1.Добавить();
    Строка.А = 2; Строка.Б= 2;
    
    ТЗ2 = новый ТаблицаЗначений;
    ТЗ2.Колонки.Добавить("А");
    ТЗ2.Колонки.Добавить("Б");
    
    Строка = ТЗ2.Добавить();
    Строка.А = 1; Строка.Б= 1;
    Строка = ТЗ2.Добавить();
    Строка.А = 2; Строка.Б= 2;
    
    РезультатСравнения = Ожидаем.Проверить2Значения(ТЗ1,ТЗ2);
    Ожидаем.Что(РезультатСравнения).ИмеетТип("Структура");
    Ожидаем.Что(РезультатСравнения.Тип).Равно(1);
    Ожидаем.Что(РезультатСравнения.Результат).Равно(Истина);
КонецПроцедуры    
Процедура ТестДолжен_Проверить_ТЗРазличаются() Экспорт
    ТЗ1 = новый ТаблицаЗначений;
    ТЗ1.Колонки.Добавить("А");
    ТЗ1.Колонки.Добавить("Б");
    
    Строка = ТЗ1.Добавить();
    Строка.А = 1; Строка.Б= 1;
    Строка = ТЗ1.Добавить();
    Строка.А = 3; Строка.Б= 4;
    
    ТЗ2 = новый ТаблицаЗначений;
    ТЗ2.Колонки.Добавить("А");
    ТЗ2.Колонки.Добавить("Б");
    
    Строка = ТЗ2.Добавить();
    Строка.А = 1; Строка.Б= 1;
    Строка = ТЗ2.Добавить();
    Строка.А = 2; Строка.Б= 2;
    

    
    РезультатСравнения = Ожидаем.Проверить2Значения(ТЗ1,ТЗ2);
    Ожидаем.Что(РезультатСравнения).ИмеетТип("Структура");
    Ожидаем.Что(РезультатСравнения.Тип).Равно(0);
    Ожидаем.Что(РезультатСравнения.Результат).Равно(
    "Строка 1, колонка А. Ожидалось 3, передано 2
|Строка 1, колонка Б. Ожидалось 4, передано 2");
КонецПроцедуры    
Процедура ТестДолжен_Проверить_ТЗРавныЧерезСтарыйИнтерфейс() Экспорт

    ТЗ1 = новый ТаблицаЗначений;
    ТЗ1.Колонки.Добавить("А");
    ТЗ1.Колонки.Добавить("Б");
    
    Строка = ТЗ1.Добавить();
    Строка.А = 1; Строка.Б= 1;
    Строка = ТЗ1.Добавить();
    Строка.А = 2; Строка.Б= 2;
    
    ТЗ2 = новый ТаблицаЗначений;
    ТЗ2.Колонки.Добавить("А");
    ТЗ2.Колонки.Добавить("Б");
    
    Строка = ТЗ2.Добавить();
    Строка.А = 1; Строка.Б= 1;
    Строка = ТЗ2.Добавить();
    Строка.А = 2; Строка.Б= 2;
    
    Ожидаем.Что(ТЗ1).Равно(ТЗ2);
КонецПроцедуры    

Процедура ТестДолжен_Проверить_ДЗРавны() Экспорт

    ДЗ1 = новый ДеревоЗначений;
    ДЗ1.Колонки.Добавить("А");
    ДЗ1.Колонки.Добавить("Б");
    
    Строка = ДЗ1.Строки.Добавить();
    Строка.А = 1; Строка.Б= 1;
    Строка1 = Строка.Строки.Добавить();
    Строка1.А = 2; Строка1.Б= 2;
    
    ДЗ2 = новый ДеревоЗначений;
    ДЗ2.Колонки.Добавить("А");
    ДЗ2.Колонки.Добавить("Б");
    
    Строка = ДЗ2.Строки.Добавить();
    Строка.А = 1; Строка.Б= 1;
    Строка1 = Строка.Строки.Добавить();
    Строка1.А = 2; Строка1.Б= 2;
    
    РезультатСравнения = Ожидаем.Проверить2Значения(ДЗ1,ДЗ2);
    Ожидаем.Что(РезультатСравнения).ИмеетТип("Структура");
    Ожидаем.Что(РезультатСравнения.Тип).Равно(1);
    Ожидаем.Что(РезультатСравнения.Результат).Равно(Истина);
КонецПроцедуры    
Процедура ТестДолжен_Проверить_ДЗРазличаются() Экспорт
    ДЗ1 = новый ДеревоЗначений;
    ДЗ1.Колонки.Добавить("А");
    ДЗ1.Колонки.Добавить("Б");
    
    Строка = ДЗ1.Строки.Добавить();
    Строка.А = 1; Строка.Б= 1;
    Строка1 = Строка.Строки.Добавить();
    Строка1.А = 2; Строка1.Б= 2;
    
    ДЗ2 = новый ДеревоЗначений;
    ДЗ2.Колонки.Добавить("А");
    ДЗ2.Колонки.Добавить("Б");
    
    Строка = ДЗ2.Строки.Добавить();
    Строка.А = 1; Строка.Б= 1;
    Строка1 = Строка.Строки.Добавить();
    Строка1.А = 2; Строка1.Б= 3;
    

    
    РезультатСравнения = Ожидаем.Проверить2Значения(ДЗ1,ДЗ2);
    Ожидаем.Что(РезультатСравнения).ИмеетТип("Структура");
    Ожидаем.Что(РезультатСравнения.Тип).Равно(0);
    Ожидаем.Что(РезультатСравнения.Результат).Равно(
    "Строка 0, колонка Б. Ожидалось 2, передано 3
|Ожидали, что проверяемое значение (2) РАВНО (3)
|Строка 0, колонка Б. Ожидалось 2, передано 3
|Ожидали, что проверяемое значение (2) РАВНО (3)");
КонецПроцедуры    
Процедура ТестДолжен_Проверить_ДЗРавныЧерезСтарыйИнтерфейс() Экспорт

    ДЗ1 = новый ДеревоЗначений;
    ДЗ1.Колонки.Добавить("А");
    ДЗ1.Колонки.Добавить("Б");
    
    Строка = ДЗ1.Строки.Добавить();
    Строка.А = 1; Строка.Б= 1;
    Строка1 = Строка.Строки.Добавить();
    Строка1.А = 2; Строка1.Б= 2;
    
    ДЗ2 = новый ДеревоЗначений;
    ДЗ2.Колонки.Добавить("А");
    ДЗ2.Колонки.Добавить("Б");
    
    Строка = ДЗ2.Строки.Добавить();
    Строка.А = 1; Строка.Б= 1;
    Строка1 = Строка.Строки.Добавить();
    Строка1.А = 2; Строка1.Б= 2;
    
    Ожидаем.Что(ДЗ1).Равно(ДЗ2);
КонецПроцедуры    

