Перем КонтекстЯдра;
Перем Ожидаем;

Перем ПарсерКоманднойСтроки;
Перем ИмяКаталогаВременныхФайлов;

Процедура Инициализация(КонтекстЯдраПараметр) Экспорт
	КонтекстЯдра = КонтекстЯдраПараметр;
	Ожидаем = КонтекстЯдра.Плагин("УтвержденияBDD");
	ПарсерКоманднойСтроки = КонтекстЯдра.Плагин("ПарсерКоманднойСтроки");
КонецПроцедуры

Функция ПолучитьСписокТестов() Экспорт
	ВсеТесты = Новый Массив;
	ВсеТесты.Добавить("ТестДолжен_ПроверитьЗапускВПакетномРежиме_ТолстыйКлиент");
	//ВсеТесты.Добавить("ТестДолжен_ПроверитьЗапускВПакетномРежиме_ТонкийКлиент");
	
	Возврат ВсеТесты;
КонецФункции

Процедура ПередЗапускомТеста() Экспорт
	ИмяКаталогаВременныхФайлов = ПолучитьИмяВременногоФайла();
	СоздатьКаталог(ИмяКаталогаВременныхФайлов);
КонецПроцедуры

Процедура ПослеЗапускаТеста() Экспорт
	УдалитьФайлы(ИмяКаталогаВременныхФайлов);
КонецПроцедуры

Процедура ТестДолжен_ПроверитьЗапускВПакетномРежиме_ТолстыйКлиент() Экспорт
	ПутьКПлатформе1С = КаталогПрограммы();
	ИмяПрограмы = "1cv8.exe";
	ПутьКПлатформе1С = ПутьКПлатформе1С + ИмяПрограмы;
	
	СтрокаРежимЗапуска = " /Lru /VLru ";
	СтрокаРежимЗапуска = СтрокаРежимЗапуска + "/RunModeOrdinary ";
	СтрокаРежимЗапуска = СтрокаРежимЗапуска + "/DisableStartupMessages ";
	
	СтрокаСоединения = СтрокаСоединенияИнформационнойБазы();		
	ПутьКФайловойБазе = НСтр(СтрокаСоединения, "File");
	Если НЕ ПустаяСтрока(ПутьКФайловойБазе) Тогда
		СтрокаСоединения = " /F """ + ПутьКФайловойБазе+"""";
	Иначе
		СтрокаСоединения = " /S " + НСтр(СтрокаСоединения, "Srvr") + "\" + НСтр(СтрокаСоединения, "Ref");
	КонецЕсли;
	ТекущийПользователь = ПользователиИнформационнойБазы.ТекущийПользователь();
	ИмяПользователя = ТекущийПользователь.Имя;
	СтрокаСоединения = СтрокаСоединения + " /N """ + ИмяПользователя + """";
	
	ФайлЛогаUI = Новый Файл(ИмяКаталогаВременныхФайлов + "\log.txt");
	СтрокаЛогированияUI = " /LogUI /Out """ + ФайлЛогаUI.ПолноеИмя + """";
	
	ТекущийФайл = Новый Файл(ЭтотОбъект.ИспользуемоеИмяФайла);
	ФайлСТестами = Новый Файл(ТекущийФайл.Путь + "Тесты_СистемаПлагинов.epf");
	ФайлаСОтчетомОТестировании = Новый Файл(ИмяКаталогаВременныхФайлов + "\report.xml");
	СтрокаПараметров = " /C """ + ПарсерКоманднойСтроки.ВозможныеКлючи.xddRun + " ЗагрузчикФайла """"" + ФайлСТестами.ПолноеИмя + """"";"
		+ ПарсерКоманднойСтроки.ВозможныеКлючи.xddShutdown + ";"
		+ ПарсерКоманднойСтроки.ВозможныеКлючи.xddReport + " ГенераторОтчетаJUnitXML """"" + ФайлаСОтчетомОТестировании.ПолноеИмя +  """"""
		+ """";
	
	СтрокаКоманды = """" + ПутьКПлатформе1С + """";
	СтрокаКоманды = СтрокаКоманды + СтрокаРежимЗапуска;
	СтрокаКоманды = СтрокаКоманды + СтрокаСоединения;
	СтрокаКоманды = СтрокаКоманды + " /Execute " + КонтекстЯдра.ИспользуемоеИмяФайла;
	СтрокаКоманды = СтрокаКоманды + СтрокаЛогированияUI;
	СтрокаКоманды = СтрокаКоманды + СтрокаПараметров;
	
	ЗапуститьПриложение(СтрокаКоманды, , Истина);
	
	Ожидаем.Что(ФайлЛогаUI.Существует()).ЭтоИстина();
	Ожидаем.Что(ФайлаСОтчетомОТестировании.Существует()).ЭтоИстина();
КонецПроцедуры
