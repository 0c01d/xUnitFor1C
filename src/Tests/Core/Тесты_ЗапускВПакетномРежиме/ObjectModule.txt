Перем КонтекстЯдра;
Перем Ожидаем;

Перем ПарсерКоманднойСтроки;
Перем ИмяКаталогаВременныхФайлов;
Перем ФайлЛогаUI;
Перем ФайлаСОтчетомОТестировании;

Процедура Инициализация(КонтекстЯдраПараметр) Экспорт
	КонтекстЯдра = КонтекстЯдраПараметр;
	Ожидаем = КонтекстЯдра.Плагин("УтвержденияBDD");
	ПарсерКоманднойСтроки = КонтекстЯдра.Плагин("ПарсерКоманднойСтроки");
КонецПроцедуры

Функция ПолучитьСписокТестов() Экспорт
	ВсеТесты = Новый Массив;
	ВсеТесты.Добавить("ТестДолжен_ПроверитьЗапускВПакетномРежиме_ТолстыйКлиент");
	ВсеТесты.Добавить("ТестДолжен_ПроверитьЗапускВПакетномРежиме_ТонкийКлиент");
	
	Возврат ВсеТесты;
КонецФункции

Процедура ПередЗапускомТеста() Экспорт
	ИмяКаталогаВременныхФайлов = ПолучитьИмяВременногоФайла();
	СоздатьКаталог(ИмяКаталогаВременныхФайлов);
	
	ФайлЛогаUI = Новый Файл(ИмяКаталогаВременныхФайлов + "\log.txt");
	ФайлаСОтчетомОТестировании = Новый Файл(ИмяКаталогаВременныхФайлов + "\report.xml");
КонецПроцедуры

Процедура ПослеЗапускаТеста() Экспорт
	УдалитьФайлы(ИмяКаталогаВременныхФайлов);
КонецПроцедуры

Процедура ТестДолжен_ПроверитьЗапускВПакетномРежиме_ТолстыйКлиент() Экспорт
	ТекущийФайл = Новый Файл(ЭтотОбъект.ИспользуемоеИмяФайла);
	ФайлСТестами = Новый Файл(ТекущийФайл.Путь + "Тесты_СистемаПлагинов.epf");
	СтрокаПараметров = " /C """ + ПарсерКоманднойСтроки.ВозможныеКлючи.xddRun + " ЗагрузчикФайла """"" + ФайлСТестами.ПолноеИмя + """"";"
		+ ПарсерКоманднойСтроки.ВозможныеКлючи.xddShutdown + ";"
		+ ПарсерКоманднойСтроки.ВозможныеКлючи.xddReport + " ГенераторОтчетаJUnitXML """"" + ФайлаСОтчетомОТестировании.ПолноеИмя +  """"""
		+ """";
	
	ВыполнитьПакетныйЗапуск(РежимЗапускаКлиентскогоПриложения.ОбычноеПриложение, СтрокаПараметров);
	
	Ожидаем.Что(ФайлЛогаUI.Существует()).ЭтоИстина();
	Ожидаем.Что(ФайлаСОтчетомОТестировании.Существует()).ЭтоИстина();
КонецПроцедуры

Процедура ВыполнитьПакетныйЗапуск(РежимЗапуска, СтрокаПараметров)
	ПутьКПлатформе1С = ПолучитьПутьКПлатформе1С(РежимЗапуска);
	ВсякиеКлючи = " /Lru /VLru /DisableStartupMessages ";
	СтрокаРежимЗапуска = ПолучитьСтрокаРежимаЗапуска(РежимЗапуска);
	СтрокаСоединения = ПолучитьСтрокуСоединения();
	СтрокаЛогированияUI = " /LogUI /Out """ + ФайлЛогаUI.ПолноеИмя + """";
	
	СтрокаКоманды = """" + ПутьКПлатформе1С + """";
	СтрокаКоманды = СтрокаКоманды + ВсякиеКлючи;
	СтрокаКоманды = СтрокаКоманды + СтрокаРежимЗапуска;
	СтрокаКоманды = СтрокаКоманды + СтрокаСоединения;
	СтрокаКоманды = СтрокаКоманды + " /Execute " + КонтекстЯдра.ИспользуемоеИмяФайла;
	СтрокаКоманды = СтрокаКоманды + СтрокаЛогированияUI;
	СтрокаКоманды = СтрокаКоманды + СтрокаПараметров;
	
	ЗапуститьПриложение(СтрокаКоманды, , Истина);
КонецПроцедуры

Функция ПолучитьПутьКПлатформе1С(РежимЗапуска)
	Если РежимЗапуска = РежимЗапускаКлиентскогоПриложения.УправляемоеПриложение Тогда
		ИмяПрограмы = "1cv8c.exe";
	ИначеЕсли РежимЗапуска = РежимЗапускаКлиентскогоПриложения.ОбычноеПриложение Тогда	
		ИмяПрограмы = "1cv8.exe";
	КонецЕсли;
	ПутьКПлатформе1С = КаталогПрограммы() + ИмяПрограмы;
	
	Возврат ПутьКПлатформе1С;
КонецФункции

Функция ПолучитьСтрокаРежимаЗапуска(РежимЗапуска)
	Перем СтрокаРежимЗапуска;
	Если РежимЗапуска = РежимЗапускаКлиентскогоПриложения.УправляемоеПриложение Тогда
		СтрокаРежимЗапуска = " /RunModeManagedApplication ";
	ИначеЕсли РежимЗапуска = РежимЗапускаКлиентскогоПриложения.ОбычноеПриложение Тогда
		СтрокаРежимЗапуска = " /RunModeOrdinary ";
	КонецЕсли;
	
	Возврат СтрокаРежимЗапуска;
КонецФункции

Функция ПолучитьСтрокуСоединения()
	СтрокаСоединения = СтрокаСоединенияИнформационнойБазы();		
	ПутьКФайловойБазе = НСтр(СтрокаСоединения, "File");
	Если НЕ ПустаяСтрока(ПутьКФайловойБазе) Тогда
		СтрокаСоединения = " /F """ + ПутьКФайловойБазе+"""";
	Иначе
		СтрокаСоединения = " /S " + НСтр(СтрокаСоединения, "Srvr") + "\" + НСтр(СтрокаСоединения, "Ref");
	КонецЕсли;
	ТекущийПользователь = ПользователиИнформационнойБазы.ТекущийПользователь();
	ИмяПользователя = ТекущийПользователь.Имя;
	СтрокаСоединения = СтрокаСоединения + " /N """ + ИмяПользователя + """";
	
	Возврат СтрокаСоединения;
КонецФункции

Процедура ТестДолжен_ПроверитьЗапускВПакетномРежиме_ТонкийКлиент() Экспорт
	ТекущийФайл = Новый Файл(ЭтотОбъект.ИспользуемоеИмяФайла);
	ФайлСТестами = Новый Файл(ТекущийФайл.Путь + "Тесты_СистемаПлагинов.epf");
	СтрокаПараметров = " /C """ + ПарсерКоманднойСтроки.ВозможныеКлючи.xddRun + " ЗагрузчикФайла """"" + ФайлСТестами.ПолноеИмя + """"";"
		+ ПарсерКоманднойСтроки.ВозможныеКлючи.xddShutdown + ";"
		+ ПарсерКоманднойСтроки.ВозможныеКлючи.xddReport + " ГенераторОтчетаJUnitXML """"" + ФайлаСОтчетомОТестировании.ПолноеИмя +  """"""
		+ """";
	
	ВыполнитьПакетныйЗапуск(РежимЗапускаКлиентскогоПриложения.УправляемоеПриложение, СтрокаПараметров);
	
	Ожидаем.Что(ФайлаСОтчетомОТестировании.Существует()).ЭтоИстина();
КонецПроцедуры
