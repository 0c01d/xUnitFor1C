&НаКлиенте
Перем КонтекстЯдра;
&НаКлиенте
Перем Ожидаем;
&НаКлиенте
Перем ПарсерКоманднойСтроки;

//{ основная процедура для юнит-тестирования xUnitFor1C

&НаКлиенте
Процедура Инициализация(КонтекстЯдраПараметр) Экспорт
	КонтекстЯдра = КонтекстЯдраПараметр;
	Ожидаем = КонтекстЯдра.Плагин("УтвержденияBDD");
	ПарсерКоманднойСтроки = КонтекстЯдра.Плагин("ПарсерКоманднойСтроки");
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьНаборТестов(НаборТестов) Экспорт
	НаборТестов.Добавить("ТестДолжен_ПрочитатьНастройкиПереданныеВКоманднойСтроке");
	НаборТестов.Добавить("ТестДолжен_ПрочитатьНастройкуПереданнуюВКоманднойСтроке");
КонецПроцедуры
//}

//{ блок юнит-тестов - сами тесты

&НаКлиенте
Процедура ПередЗапускомТеста() Экспорт
	ОбновитьПовторноИспользуемыеЗначения();
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗапускаТеста() Экспорт
КонецПроцедуры

&НаКлиенте
Процедура ТестДолжен_ПрочитатьНастройкиПереданныеВКоманднойСтроке() Экспорт
	Если СтрНайти(ПараметрЗапуска, ПарсерКоманднойСтроки.Объект.ВозможныеКлючи.xddRun) = 0 Тогда
		КонтекстЯдра.ПропуститьТест("Пропускаю, потому что не передан параметр-конфиг в командной строке");
	КонецЕсли;
	
	ПлагинНастроек = КонтекстЯдра.Плагин("Настройки");
	Настройки = ПлагинНастроек.ПолучитьНастройки();
	Ожидаем.Что(Настройки, "Не получили нужный тип настройки").ИмеетТип(Тип("ФиксированнаяСтруктура"));
	
	ПутьНастройки = "Тесты_Настройки";
	Настройка = Настройки[ПутьНастройки];
	Ожидаем.Что(Настройка, "Не получили нужный текст настройки").Равно("значение для Тесты_Настройки");
КонецПроцедуры

&НаКлиенте
Процедура ТестДолжен_ПрочитатьНастройкуПереданнуюВКоманднойСтроке() Экспорт
	Если СтрНайти(ПараметрЗапуска, ПарсерКоманднойСтроки.Объект.ВозможныеКлючи.xddRun) = 0 Тогда
		КонтекстЯдра.ПропуститьТест("Пропускаю, потому что не передан параметр-конфиг в командной строке");
	КонецЕсли;
	
	ПлагинНастроек = КонтекстЯдра.Плагин("Настройки");
	
	ПутьНастройки = "Тесты_Настройки";
	Настройка = ПлагинНастроек.ПолучитьНастройку(ПутьНастройки);
	
	Ожидаем.Что(Настройка, "Не получили нужное значение настройки").Равно("значение для Тесты_Настройки");
КонецПроцедуры

//} конец блока юнит-тестов
