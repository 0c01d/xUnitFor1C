#Если ТолстыйКлиентОбычноеПриложение Тогда
	
Перем КонтекстЯдра;
Перем Ожидаем;
Перем ПарсерКоманднойСтроки;

//{ основные процедуры для юнит-тестирования xUnitFor1C

Процедура Инициализация(КонтекстЯдраПараметр) Экспорт
	КонтекстЯдра = КонтекстЯдраПараметр;
	Ожидаем = КонтекстЯдра.Плагин("УтвержденияBDD");
	ПарсерКоманднойСтроки = КонтекстЯдра.Плагин("ПарсерКоманднойСтроки");
КонецПроцедуры

Процедура ЗаполнитьНаборТестов(НаборТестов) Экспорт
	НаборТестов.Добавить("ТестДолжен_ПрочитатьНастройкиПереданныеВКоманднойСтроке");
	НаборТестов.Добавить("ТестДолжен_ПрочитатьНастройкуПереданнуюВКоманднойСтроке");
КонецПроцедуры
//}

//{ блок юнит-тестов - сами тесты

Процедура ПередЗапускомТеста() Экспорт
	Если СтрНайти(ПараметрЗапуска, ПарсерКоманднойСтроки.ВозможныеКлючи.xddRun) = 0 Тогда
		КонтекстЯдра.ПропуститьТест("Пропускаю, потому что не передан параметр-конфиг в командной строке");
	КонецЕсли;
	
	НачатьТранзакцию();	
КонецПроцедуры

Процедура ПослеЗапускаТеста() Экспорт
	Если ТранзакцияАктивна() Тогда
		ОтменитьТранзакцию();
	КонецЕсли; 
КонецПроцедуры
	
Процедура ТестДолжен_ПрочитатьНастройкиПереданныеВКоманднойСтроке() Экспорт
	
	ПлагинНастроек = КонтекстЯдра.Плагин("Настройки");
	Настройки = ПлагинНастроек.ПолучитьНастройки();
	Ожидаем.Что(Настройки, "Не получили нужный тип настроек").ИмеетТип(Тип("ФиксированнаяСтруктура"));
	
	ПутьНастройки = "Тесты_Настройки";
	Настройка = Настройки[ПутьНастройки];
	
	ПроверитьНастройку(Настройка);
КонецПроцедуры

Процедура ТестДолжен_ПрочитатьНастройкуПереданнуюВКоманднойСтроке() Экспорт
	
	ПлагинНастроек = КонтекстЯдра.Плагин("Настройки");
	
	ПутьНастройки = "Тесты_Настройки";
	Настройка = ПлагинНастроек.ПолучитьНастройку(ПутьНастройки);
	
	ПроверитьНастройку(Настройка);
КонецПроцедуры

Процедура ПроверитьНастройку(Знач Настройка)
	Ожидаем.Что(Настройка, "Не получили нужный тип настройки").ИмеетТип(Тип("Структура"));
	Ожидаем.Что(Настройка.Свойство("Ключ"), "Не найден нужный ключ у настройки ").ЭтоИстина();
	Ожидаем.Что(Настройка.Ключ, "Не получили нужное значение настройки").Равно("значение для Тесты_Настройки");
КонецПроцедуры

#КонецЕсли 

//} 
