Перем мТипыСтроки;

Процедура ПриОткрытии()
	ТолькоСТестами = Истина;
	
	ЗаполнитьДерево();
	ОчиститьДерево(ДеревоПодсистем);
КонецПроцедуры

Процедура ДеревоПодсистемПриВыводеСтроки(Элемент, ОформлениеСтроки, ДанныеСтроки)
	ОформлениеСтроки.Ячейки.Имя.УстановитьКартинку(мТипыСтроки[ДанныеСтроки.ТипСтроки]);		
КонецПроцедуры

Процедура ЗаполнитьДерево()
	Для каждого Подсистема из Метаданные.Подсистемы цикл
		ДобавитьПодсистемуВДерево(Подсистема,ДеревоПодсистем);
	КонецЦикла;
КонецПроцедуры

Функция ДобавитьПодсистемуВДерево(ТекПодсистема,СтрокаДерева)
	СтрокаПодсистемы = СтрокаДерева.Строки.Добавить();
	СтрокаПодсистемы.Имя = ТекПодсистема.Имя;
	СтрокаПодсистемы.Путь = "Метаданные."+СтрЗаменить(ТекПодсистема.ПолноеИмя(), "Подсистема.", "Подсистемы.");
	СтрокаПодсистемы.ТипСтроки = "Подсистема";
	Для Каждого Подсистема из ТекПодсистема.Подсистемы Цикл 
		ДобавитьПодсистемуВДерево(Подсистема,СтрокаПодсистемы);
	КонецЦикла;
	
	Для каждого ОбъектМетаданных из ТекПодсистема.Состав цикл
		Если Лев(ОбъектМетаданных.ПолноеИмя(),15) = "Обработка.Тест_" Тогда
			СтрокаТеста = СтрокаПодсистемы.Строки.Добавить();
			СтрокаТеста.Имя = ОбъектМетаданных.Имя;
			СтрокаТеста.Путь = "Метаданные."+СтрЗаменить(ОбъектМетаданных.ПолноеИмя(), "Обработка.", "Обработки.");
			СтрокаТеста.ТипСтроки = "Тест";
			СтрокаТеста.ИмеетТесты = Истина;
			ПометитьРодителя(СтрокаТеста);
		КонецЕсли;
	КонецЦикла;
КонецФункции

// Устанавливает всем родителям по иерархии признак наличия тестов в иерархии
//
Процедура ПометитьРодителя(ТекСтрока)
	СтрокаРодитель = ТекСтрока.Родитель;
	Если СтрокаРодитель <> неопределено Тогда
		СтрокаРодитель.ИмеетТесты = Истина;
		ПометитьРодителя(СтрокаРодитель);
	КонецЕсли;
КонецПроцедуры

// Убирает из дерева все строки, в иерархии которых нет тестов
//
Процедура ОчиститьДерево(СтрокаДерева)
	сч = СтрокаДерева.Строки.Количество()-1;
	Пока сч>=0 цикл
		Строка = СтрокаДерева.Строки[сч];
		Если Не Строка.ИмеетТесты Тогда
			СтрокаДерева.Строки.удалить(сч);
		Иначе
			ОчиститьДерево(Строка);
		КонецЕсли;
		сч = сч - 1;
	КонецЦикла;
КонецПроцедуры

Процедура ДеревоПодсистемВыбор(Элемент, ВыбраннаяСтрока, Колонка, СтандартнаяОбработка)
	СтандартнаяОбработка = ложь;
	ЭтаФорма.Закрыть(ВыбраннаяСтрока.Путь);
КонецПроцедуры

//{ Типы строк дерева тестов
мТипыСтроки = Новый Структура;
мТипыСтроки.Вставить("Подсистема",ЭлементыФормы.ПолеКартинкиПодсистема.Картинка);
мТипыСтроки.Вставить("Тест",ЭлементыФормы.ПолеКартинкиТест.Картинка);
мТипыСтроки = новый ФиксированнаяСтруктура(мТипыСтроки);
//} Типы строк дерева тестов